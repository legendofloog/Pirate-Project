EventPointerTable(Chapter3EventsID, PointerList)

//Flags
/*  1 - Alonso BQ
    2 - Alonso DQ
    7 - Alonso BQ 2
    8 - Dalila BQ
    9 - Dalila BQ 2 
    10 - Dalila DQ //finished to this point
    11 - Alonso/Lua BQ
    12 - Alonso/Desiderio BQ
    13 - Alonso/Orlando BQ
    14 - Alonso/Carmen BQ
    15 - Alonso/Ximo BQ
    16 - Alonso/Ernest BQ
    17 - Dalila/Pilar BQ
    18 - Dalila/Razvan BQ
    19 - Dalila/Rahel BQ
    20 - Dalila/Fata BQ

*/

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
//TurnEventPlayer(0, EndingScene, 1)
TurnEventPlayer(0, ReinforcementWarning, 5)
TurnEventPlayer(0, Reinforcements1, 6)
TurnEventPlayer(0, Reinforcements2, 8)
TurnEventPlayer(0, Reinforcements3, 9)
TurnEventPlayer(0, ReinforcementsAllTogetherNow, 12)
TurnEventPlayer(0, TurnLimit, 13) //
END_MAIN

ReinforcementWarning:
CameraCursor(AlonsoChapter3)
Text(Ch3AlonsoReinforcementWarning)
NoFade
ENDA

Reinforcements1:
SpawnReinforcements(ReinforcementGroup1)
NoFade
ENDA

ReinforcementGroup1:
UNIT Blackheart Brigand 0 Level(15,Enemy,True) [13,27] 0 0 1 REDA12R27 [SteelAxe] PursueWithoutHeedAI
UNIT Blackheart Gunner 0 Level(15,Enemy,True) [13,27] 0 0 1 REDA13R26 [IronMusket] PursueWithoutHeedAI
UNIT Blackheart Myrmidon 0 Level(15,Enemy,True) [20,27] 0 0 1 REDA21R27 [IronSword] PursueWithoutHeedAI
UNIT Blackheart Shaman 0 Level(15,Enemy,True) [20,27] 0 0 1 REDA20R26 [Elfire] PursueWithoutHeedAI
UNIT

Reinforcements2:
SpawnReinforcements(ReinforcementGroup2)
NoFade
ENDA

ReinforcementGroup2:
UNIT Blackheart Warden 0 Level(15,Enemy,True) [13,21] 0 0 1 REDA13R21 [SteelSword, SteelLance] PursueWithoutHeedAI
UNIT Blackheart Vanguard 0 Level(15,Enemy,True) [20,21] 0 0 1 REDA20R21 [SteelAxe, SteelLance] PursueWithoutHeedAI
UNIT

Reinforcements3:
SpawnReinforcements(ReinforcementGroup3)
NoFade
ENDA

ReinforcementGroup3:
UNIT Blackheart Thief 0 Level(15,Enemy,True) [15,18] 0 0 1 REDA15R17 [Bloodletter] PursueWithoutHeedAI
UNIT Blackheart Hunter 0 Level(15,Enemy,True) [15,18] 0 0 1 REDA16R18 [ShiningBow] PursueWithoutHeedAI
UNIT Blackheart Thief 0 Level(15,Enemy,True) [18,18] 0 0 1 REDA18R17 [Stiletto] PursueWithoutHeedAI
UNIT Blackheart Hunter 0 Level(15,Enemy,True) [18,18] 0 0 1 REDA17R18 [Tomereaver] PursueWithoutHeedAI
UNIT

ReinforcementsAllTogetherNow:
SpawnReinforcements(ReinforcementGroup1)
SpawnReinforcements(ReinforcementGroup2)
SpawnReinforcements(ReinforcementGroup3)
NoFade
ENDA

TurnLimit:
CameraCursor(AlonsoChapter3)
Text(AlonsoVictoryText)
CameraCursor(Lua)
Text(LuaDefeatText)
CALL GameOverStart
NoFade
ENDA

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Chest(SilverMusket, 3, 16)
Chest(Lancereaver, 32, 15)
DurabilityChest(SkillScroll,LungeID,16,10)
END_MAIN

MiscBasedEvents:
AFEV 25 AlonsoDefeat 2
AFEV 26 DalilaDefeat 10
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

AlonsoDefeat:
CHECK_EVENTID 10 //check if dalila was defeated
BEQ 1 s0 sC
    ENUT 3
	CALL EndingScene
LABEL 1
NoFade
ENDA

DalilaDefeat:
CHECK_EVENTID 2 //check if alonso was defeated
BEQ 1 s0 sC
    ENUT 3
	CALL EndingScene
LABEL 1
NoFade
ENDA

Units1:
UNIT Lua 0 0 Level(10,Ally,False) [21,40] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [30,1] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(10,Ally,False) [3,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Razvan 0 0 Level(10,Ally,False) [12,42] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
LoadUnit(21,41)
LoadUnit(20,40)
LoadUnit(13,42)
LoadUnit(12,41)

LoadUnit(31,1)
LoadUnit(30,2)
LoadUnit(31,2)

LoadUnit(2,2)
LoadUnit(2,3)
LoadUnit(3,3)
UNIT

LeftSideUnits: //axes, guns, knives
//turn 1
UNIT Blackheart Brigand 0 Level(15,Enemy,True) [9,39] 0 0 0 0 [SteelAxe] PursueWithoutHeedAI
UNIT Blackheart Thief 0 Level(15,Enemy,True) [10,38] 0 0 0 0 [SteelKnife] PursueWithoutHeedAI

UNIT TorchUnit Rogue 0 Level(16,Enemy,True) [7,3] 0 0 0 0 [Peshkatz, SilverKnife] GroupSixAI
UNIT Blackheart Brigand 0 Level(15,Enemy,True) [8,4] 0 0 0 0 [SteelAxe] GroupSixAI

//turn 2
UNIT Blackheart Thief 0 Level(15,Enemy,True) [3,34] 0 0 0 0 [SteelKnife] GroupSevenAI
UNIT TorchUnit Marksman 0 Level(16,Enemy,True) [5,35] 0 0 0 0 [SteelMusket, Pistol] GroupSevenAI

UNIT Blackheart Brigand 0 Level(15,Enemy,True) [7,12] 0 0 0 0 [SteelAxe] GroupTenAI
UNIT TorchUnit Marksman 0 Level(16,Enemy,True) [8,11] 0 0 0 0 [SilverMusket, Blunderbuss] GroupTenAI

//turn 3
UNIT Blackheart Gunner 0 Level(15,Enemy,True) [9,29] 0 0 0 0 [SteelMusket] GroupNineAI
UNIT Blackheart Thief 0 Level(15,Enemy,True) [8,30] 0 0 0 0 [SteelKnife] GroupNineAI

//turn 4?
UNIT Blackheart Gunner 0 Level(15,Enemy,True) [7,17] 0 0 0 0 [SteelMusket] GroupEightAI
UNIT Blackheart Brigand 0 Level(15,Enemy,True) [8,18] 0 0 0 0 [SteelAxe] GroupEightAI


//turn 5-6?
UNIT TorchUnit Berserker 0 Level(16,Enemy,True) [2,17] 0 0 0 0 [SilverAxe] GroupOneAI
UNIT Blackheart Gunner 0 Level(15,Enemy,True) [1,18] 0 0 0 0 [Culverin] GroupOneAI
UNIT

CenterBoatUnits: //armored units, staffers, and bows
UNIT Dalila Assassin_F 0 Level(18, Enemy, True) [16,12] 0 0 0 0 [Scorpio, SilverKnife, Pursuer] BossAI
UNIT AlonsoChapter3 Adventurer 0 Level(20, Enemy, True) [17,13] 0 0 0 0 [ShiningBow, BraveBow, Sleep] BossAI

UNIT BlackheartElite Sniper 0 Level(15, Enemy, True) [15,17] 0 0 0 0 [SilverBow, Greatbow] BossAI
UNIT BlackheartElite Sniper 0 Level(15, Enemy, True) [18,17] 0 0 0 0 [SilverBow, Greatbow] BossAI
UNIT BlackheartElite Sniper 0 Level(15, Enemy, True) [15,8] 0 0 0 0 [SilverBow, Greatbow] BossAI
UNIT BlackheartElite Sniper 0 Level(15, Enemy, True) [18,8] 0 0 0 0 [SilverBow, Greatbow] BossAI

//big ship guards
UNIT TorchUnit General 0 Level(16,Enemy,True) [16,6] 0 0 0 0 [Spear, SteelAxe] BossAI
UNIT TorchUnit General 0 Level(16,Enemy,True) [17,6] 0 0 0 0 [FlameSword, Pike] BossAI

UNIT TorchUnit Dreadnought 0 Level(16,Enemy,True) [19,14] 0 0 0 0 [SilverBow, Bloodaxe] BossAI
UNIT TorchUnit General 0 Level(16,Enemy,True) [19,15] 0 0 0 0 [Spear, SilverSword] BossAI
UNIT TorchUnit General 0 Level(16,Enemy,True) [14,14] 0 0 0 0 [Spear, SilverAxe] BossAI
UNIT TorchUnit Dreadnought 0 Level(16,Enemy,True) [14,15] 0 0 0 0 [SilverAxe, Pursuer] BossAI

UNIT TorchUnit Baron 0 Level(16,Enemy,True) [16,22] 0 0 0 0 [Arcfire, Peacekeeper] BossAI
UNIT TorchUnit Baron 0 Level(16,Enemy,True) [17,22] 0 0 0 0 [SilverSword, Thani] BossAI
UNIT
//turn 1

RightSideUnits: //lances, swords, tomes
//turn 1
UNIT Blackheart Deserter 0 Level(15,Enemy,True) [27,2] 0 0 0 0 [SteelLance] PursueWithoutHeedAI
UNIT Blackheart Mercenary 0 Level(15,Enemy,True) [28,3] 0 0 0 0 [SteelSword] PursueWithoutHeedAI

UNIT Blackheart Deserter 0 Level(15,Enemy,True) [23,37] 0 0 0 0 [SteelLance] PursueWithoutHeedAI
UNIT Blackheart Shaman 0 Level(15,Enemy,True) [24,38] 0 0 0 0 [Elfire] PursueWithoutHeedAI

//turn 2
UNIT TorchUnit Halberdier 0 Level(16,Enemy,True) [28,8] 0 0 0 0 [SilverLance] GroupThreeAI
UNIT Blackheart Shaman 0 Level(15,Enemy,True) [29,9] 0 0 0 0 [Elfire] GroupThreeAI

UNIT Blackheart Deserter 0 Level(15,Enemy,True) [28,35] 0 0 0 0 [SteelLance, Javelin] GroupFiveAI
UNIT TorchUnit Swordmaster 0 Level(16,Enemy,True) [29,36] 0 0 0 0 [SilverSword, Onigiri] GroupFiveAI

//turn 3
UNIT Blackheart Mercenary 0 Level(15,Enemy,True) [24,30] 0 0 0 0 [SteelBlade] AttackInRangeAI
UNIT Blackheart Soldier 0 Level(15,Enemy,True) [25,31] 0 0 0 0 [SteelLance] AttackInRangeAI

//turn 4
UNIT Blackheart Deserter 0 Level(15,Enemy,True) [26,16] 0 0 0 0 [SteelLance] GroupFourAI
UNIT TorchUnit Champion 0 Level(16,Enemy,True) [27,17] 0 0 0 0 [SteelBlade, Spear] GroupFourAI

//(turn 5-6?)
UNIT Blackheart Mercenary 0 Level(15,Enemy,True) [31,19] 0 0 0 0 [Wingclipper] GroupTwoAI
UNIT TorchUnit Slayer 0 Level(16,Enemy,True) [32,18] 0 0 0 0 [FlameSword, Arcfire] GroupTwoAI
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

LuaC3Group:
UNIT Lua 0 0 Level(10,Ally,False) [14,14] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

PilarC3Group:
UNIT Pilar 0 0 Level(10,Ally,False) [16,23] 0 0 1 REDA14R15 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

RahelOrlandoC3Group:
UNIT Rahel 0 0 Level(10,Ally,False) [16,23] 0 0 1 REDA15R14 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Orlando 0 0 Level(10,Ally,False) [16,23] 0 0 1 REDA15R15 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

AlonsoDalilaC3Group:
UNIT Alonso 0 0 Level(10,Enemy,False) [17,13] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Dalila 0 0 Level(10,Enemy,False) [16,13] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

LuaSecondC3Group:
UNIT Lua 0 0 Level(10,Ally,False) [17,37] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

RazvanPilarDesiderioC3Group:
UNIT Razvan 0 0 Level(10,Ally,False) [12,42] 0 0 1 REDA16R37 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [30,1] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(10,Ally,False) [3,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

BeginningScene:
UnsetAllHubChapterFlags
ENUF LuaInAHubFlag
LOMA HubB
CAMERA_CENTERED [14,14]
TILECHANGE 3 //load the below deck in the center
LoadCutsceneUnits(LuaC3Group)
FADU 15
Text(Ch3BeginningScene1_1_Pre)
LoadCutsceneUnits(PilarC3Group)
MUSC MeetTheCrewID
Text(Ch3BeginningScene1_1)
FadeOutMusic
LoadCutsceneUnits(RahelOrlandoC3Group)
MUSC AvastMeHeartiesID
Text(Ch3BeginningScene1_2)
MOVE 15 Pilar [16,22]
ENUN 
Text(Ch3BeginningScene1_3)
FADI 15
FadeOutMusic
RemoveAll
LoadCutsceneUnits(AlonsoDalilaC3Group)
LOMA Chapter3
CAMERA_CENTERED [16,13]
STAL 60
FADU 15
FlashCursor(Alonso,60)
FlashCursor(Dalila,60)
MUSC SwabbieTalkID
Text(Ch3BeginningScene2)
LoadCutsceneUnits(LuaSecondC3Group)
FadeOutMusic
CamOn
MOVE 15 Alonso [16,29]
ENUN
CAMERA_CENTERED [16,33]
FlashCursor(Alonso,60)
FlashCursor(Lua,60)
MUSC ABlackheartsPrideID // scene change
Text(Ch3BeginningScene3_1)
CamOff
FadeOutMusic
FADI 15
REMOVEALL_RED
EARTHQUAKE 0 True
STAL 30
EARTHQUAKE 1 True
STAL 30
EARTHQUAKE 0 True
STAL 30
EARTHQUAKE 1 True
STAL 30
EARTHQUAKE_END
TILECHANGE 0 //the one with all the bridges showing up
LoadEnemies(LeftSideUnits)
LoadEnemies(CenterBoatUnits)
LoadEnemies(RightSideUnits)
CAMERA_CENTERED Lua
FADU 15
LoadCutsceneUnits(RazvanPilarDesiderioC3Group)
MUSC FoolsBoldID
Text(Ch3BeginningScene3_2)
CameraCursor(Pilar)
Text(Ch3BeginningScene3_3Pilar)
CameraCursor(Desiderio)
Text(Ch3BeginningScene3_3Desiderio)

STAL 5
FADI 15
GotoPrepScreen
NoFade
ENDA

EndingScene1Group:
UNIT Razvan 0 0 Level(10,Ally,False) [18,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Lua 0 0 Level(10,Ally,False) [17,15] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(10,Ally,False) [15,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [16,15] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT Alonso 0 0 Level(10,Enemy,False) [17,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Dalila 0 0 Level(10,Enemy,False) [16,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

EnemyBoat:
UNIT Alonso Fleet 0 Level(10,Enemy,False) [17,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

EndingScene:
FADI 15
FadeOutMusic
RemoveAll
LoadCutsceneUnits(EndingScene1Group)
CAMERA_CENTERED Lua
SVAL s1 1
SET_HP Alonso
SET_HP Dalila
STAL 60
FADU 15
MUSC WindAmbienceID
Text(Ch3EndingScene1)
MOVE 20 Pilar [14, 40]
MOVE 20 Razvan [14, 41]
ENUN
// some eventing
Text(Ch3EndingScene1_2)
MUSC ABlackheartsPrideID
TEXTSTART 
TEXTSHOW Ch3EndingScene2
TEXTEND
MOVE 20 Lua [17, 14]
MOVE 20 Desiderio [16, 14]
ENUN
TEXTCONT
TEXTEND
REMA

FadeOutMusic
TILECHANGE 1
SOUN SnagDestructionSoundID //snag destruction sound
REMOVEUNIT Alonso
SOUN GuyFallingSoundID
REMOVEUNIT Dalila
SOUN GuyFallingSoundID
ENUN
FlashCursor(17,12,60)
LoadCutsceneUnits(EnemyBoat)
FlashCursor(Alonso,60)
REMOVEUNIT Alonso
SOUN PegasusFlyingSoundID
Text(Ch3EndingScene3)
MUSC FinInTheCloudsID
CamOn
MOVE 15 Lua [15, 40]
STAL 15
MOVE 15 Desiderio [15, 41]
ENUN

Text(Ch3EndingScene4_1)
MOVE 10 Desiderio [18,43]
ENUN
HIDEUNIT Desiderio
Text(Ch3EndingScene4_2)
MOVE 15 Razvan [17,43]
ENUN
HIDEUNIT Razvan
Text(Ch3EndingScene4_3)
MOVE 15 Lua [17,43]
ENUN
HIDEUNIT Lua
Text(Ch3EndingScene4_4)
MOVE 15 Pilar [18,43]
FADI 15
FadeOutMusic
ENUN
SHOWUNIT Lua
SHOWUNIT Desiderio
SHOWUNIT Razvan
STAL 120
SetBackground(Inn)
BROWNBOXTEXT C3BrownBoxStuff[8,8]
MUSC OrlandoThemeID
TEXTSTART
TEXTSHOW Ch3EndingScene5
TEXTEND
SOUN 0x119
STAL 30
TEXTCONT
TEXTEND
SOUN 0x11A
STAL 30
SOUN 0x11A
STAL 20
SOUND 0x11A
STAL 10
SOUND 0x11A
TEXTCONT
TEXTEND
REMA
MNC2 HubC // Placeholder until hub is implemented
NoFade
ENDB

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN