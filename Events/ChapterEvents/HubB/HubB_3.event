#define KaiTalkFlag 7
#define LeandraErnestTalkFlag 8
// 0x9-0xD: Battle Quotes
// 0xE - AREA event for Azahar fleeing (causes a Game Over)
#define AzaharEscapesFlag 0xE
#define RebelThiefDeathQuoteFlag 0xF
#define WasARebelKilledFlag 0x10
#define KaiEnemyDeathQuoteFlag 0x11

#define TC_B3CutsceneVillage 		0x0
#define TC_B3CutsceneFortOutside 	0x1
#define TC_B3CutsceneFortInterior 	0x2

EventPointerTable(HubB_3EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, B3FirstTurnEvent, 1)
//TurnEventPlayer(0, B3ThievesReinforcement1, 2)
TurnEventPlayer(0, B3KaiIntroductionEvent, 3)
TurnEventPlayer(0, MoreThievesShowUp, 3)
TurnEventPlayer(0, B3KaiFleesEvent, 4)
TurnEventPlayer(0, B3AzaharWarnsThatSheWillLeave, 9)
TurnEventEnemy(0, B3AzaharLeaves, 10)
END_MAIN

MoreThievesShowUp:
SpawnReinforcements(TopThief)
SpawnReinforcements(BotThief)
NoFade
ENDA

TopThief:
UNIT RebelThief Thief 0 Level(5,Enemy,True) [21,1] 0 0 0 0 [ChestKey] ThieveryAI
UNIT

BotThief:
UNIT RebelThief Thief 0 Level(5,Enemy,True) [19,17] 0 0 0 0 [ChestKey] ThieveryAI
UNIT

B3FirstTurnEvent:
CameraCursor(Azahar)
Text(B3AzaharIntroduction)
CenterTextbox(B3MapObjectiveExplanation)
NoFade
ENDA

B3KaiIntroductionEvent:
CHECK_ALIVE Kai
BEQ 776 s0 sC
    CHECK_ALLEGIANCE Kai
    BEQ 776 s0 sC //if it returns 0, he's a player, so skip this
        CameraCursor(Kai)
        Text(B3KaiIntroduction)
LABEL 776
NoFade
ENDA

B3KaiFleesEvent:
CHECK_ALIVE Kai
BEQ 776 s0 sC
    CHECK_ALLEGIANCE Kai
    BEQ 776 s0 sC //if it returns 0, he's a player, so skip this
    CameraCursor(Kai)
    Text(B3KaiFleeWarning)
    SetAI(Kai,0x6,0xC,0x4,0x0)
LABEL 776
NoFade
ENDA

B3AzaharWarnsThatSheWillLeave:
CameraCursor(Azahar)
Text(B3AzaharWarningText)
NoFade
ENDA

B3AzaharLeaves:
CameraCursor(Azahar)
Text(B3AzaharEscapesText_1)
MOVE 15 Azahar [28,0]
ENUN
REMOVEUNIT Azahar
STAL 10
CameraCursor(Lua)
Text(B3AzaharEscapesText_2)
CALL GameOverStart
LABEL 776
NoFade
ENDA

CharacterBasedEvents:
CharacterEventBothWays(KaiTalkFlag, KaiPilarTalkEvent, Pilar, Kai)
CharacterEventBothWays(KaiTalkFlag, KaiLuaTalkEvent, Lua, Kai)
CharacterEventBothWays(LeandraErnestTalkFlag, LeandraErnestTalkEvent, Leandra, Ernest)
END_MAIN

KaiPilarTalkEvent:
MUSC NewHandsOnDeckID
Text(B3KaiPilarTalk)
CUSA Kai
ENUT KaiEnemyDeathQuoteFlag
SetAI(Kai,0,0,0,0)
NoFade
ENDA

KaiLuaTalkEvent:
MUSC NewHandsOnDeckID
Text(B3KaiLuaTalk)
CUSA Kai
ENUT KaiEnemyDeathQuoteFlag
SetAI(Kai,0,0,0,0)
NoFade
ENDA

LeandraErnestTalkEvent:
Text(B3LeandraErnestTalk)
NoFade
ENDA

LocationBasedEvents:
Chest(SuppliesItem, 16, 7)
Chest(SuppliesItem, 25, 12)
Chest(SuppliesItem, 25, 13)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
AFEV 1 SetGenericBattleQuote 0x9
AFEV 1 SetGenericBattleQuote 0xA
AFEV 1 SetGenericBattleQuote 0xB
AFEV 1 SetGenericBattleQuote 0xC
AFEV 1 SetGenericBattleQuote 0xD
AFEV 0 RunEventAfterKillingThief RebelThiefDeathQuoteFlag
END_MAIN

SetGenericBattleQuote: //this just sets the generic battle quote to not trigger if a specific one comes first
NoFade
ENDA

RunEventAfterKillingThief:
ENUT WasARebelKilledFlag //shows that you killed a rebel, run this no matter how many are killed
DoesBattleTargetHaveSupplies
BEQ 776 s0 sC //if they do not, then we skip giving the supplies to the active unit
    GiveItem(SuppliesItem, 0xFFFF)
LABEL 776
ENUF RebelThiefDeathQuoteFlag //unset the flag so it can happen more than once
NoFade
ENDA

Traps1:
RedSigil(28,0)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
//Group 1, bottom - 7 units
UNIT Lua Captain 0 Level(10,Ally,False) [5,15] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [3,17] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [4,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [5,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [6,16] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [5,17] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [7,17] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

//Group 2, top - 5 units
UNIT Desiderio 0 0 Level(10,Ally,False) [4,0] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [6,0] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [5,1] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [7,1] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [6,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

NewUnits:
UNIT Ernest Gryphoknight 0 Level(11,Ally,True) [10,12] 0 0 0 0 [SilverLance, Pike, Javelin] NoAI
UNIT

EnemyUnits:
//end battle
UNIT Azahar Champion_F 0 Level(12, Enemy, True) [28,1] 0 0 0 0 [Spear, SilverSword] BossAI

UNIT Rebel Hero 0 Level(11,Enemy,True) [30,3] 0 0 0 0 [Peacekeeper, HandAxe] GroupOneAI
UNIT Rebel Mercenary 0 Level(10,Enemy,True) [29,2] 0 0 0 0 [SteelBlade] GroupOneAI
UNIT Rebel Myrmidon 0 Level(10,Enemy,True) [31,2] 0 0 0 0 [KillingEdge] GroupOneAI

//murderhollace enemies
UNIT Rebel Archer 0 Level(10,Enemy,True) [11,7] 0 0 0 0 [SteelBow] AttackInRangeAI
UNIT Rebel Archer 0 Level(10,Enemy,True) [13,8] 0 0 0 0 [SteelBow] AttackInRangeAI
UNIT Rebel Hunter 0 Level(10,Enemy,True) [12,0] 0 0 0 0 [Greatbow] BossAI
UNIT Rebel Hunter 0 Level(10,Enemy,True) [14,16] 0 0 0 0 [IronBow] AttackInRangeAI
UNIT Rebel Deserter 0 Level(10,Enemy,True) [12,2] 0 0 0 0 [Javelin] BossAI

UNIT Rebel Archer 0 Level(10,Enemy,True) [23,1] 0 0 0 0 [Longbow] AttackInRangeAI
UNIT Rebel Archer 0 Level(10,Enemy,True) [26,3] 0 0 0 0 [Longbow] AttackInRangeAI

//kai group (turn 2-3)
UNIT Kai Ninja 0 Level(6,Enemy,True) [17,0] 0 0 0 0 [IronKnife, RuneDagger, Vulnerary] BossAI
UNIT Rebel Rogue 0 Level(11,Enemy,True) [18,1] 0 0 0 0 [Stiletto, SteelKnife] BossAI
UNIT Rebel Thief 0 Level(11,Enemy,True) [19,0] 0 0 0 0 [SteelKnife] BossAI


//turn 1
UNIT Rebel Deserter 0 Level(10,Enemy,True) [8,12] 0 0 0 0 [SteelLance] PursueWithoutHeedAI
UNIT Rebel Thief 0 Level(10,Enemy,True) [6,10] 0 0 0 0 [IronKnife] PursueWithoutHeedAI
UNIT Rebel Deserter 0 Level(10,Enemy,True) [4,12] 0 0 0 0 [SteelLance] PursueWithoutHeedAI
UNIT Rebel Mercenary 0 Level(10,Enemy,True) [8,4] 0 0 0 0 [SteelSword] PursueWithoutHeedAI

//fleeing thieves
UNIT RebelThief Thief 0 Level(5,Enemy,True) [7,8] 0 0 0 0 [SuppliesItem] EscapeOnlyAI
UNIT RebelThief Thief 0 Level(5,Enemy,True) [16,7] 0 0 0 0 [ChestKey] ThieveryAI
UNIT RebelThief Thief 0 Level(5,Enemy,True) [11,0] 0 0 0 0 [SuppliesItem] EscapeOnlyAI
UNIT RebelThief Thief 0 Level(5,Enemy,True) [12,16] 0 0 0 0 [SuppliesItem] EscapeOnlyAI

//turn 2
UNIT Rebel Deserter 0 Level(10,Enemy,True) [17,5] 0 0 0 0 [SteelLance] AttackInTwoAI
UNIT Rebel Deserter 0 Level(10,Enemy,True) [17,11] 0 0 0 0 [SteelLance] AttackInTwoAI

//later enemies
UNIT Rebel Mercenary 0 Level(10,Enemy,True) [20,12] 0 0 0 0 [IronBlade] AttackInRangeAI
UNIT Rebel Thief 0 Level(10,Enemy,True) [21,8] 0 0 0 0 [SteelKnife] AttackInRangeAI
UNIT Rebel Thief 0 Level(10,Enemy,True) [29,16] 0 0 0 0 [SteelKnife] AttackInRangeAI
UNIT Rebel Mercenary 0 Level(10,Enemy,True) [30,12] 0 0 0 0 [IronBlade] AttackInRangeAI

UNIT Rebel Deserter 0 Level(10,Enemy,True) [24,11] 0 0 0 0 [SteelLance] AttackInRangeAI
UNIT Rebel Myrmidon 0 Level(10,Enemy,True) [26,14] 0 0 0 0 [IronSword] AttackInRangeAI
UNIT

//Cutscene Units

IntroCutsceneVillage:
UNIT Lua Captain 0 Level(10,Ally,False) [5,11] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [6,11] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [5,12] 0x0 0x0 0 0 [0x0] NoAI
UNIT Desiderio 0 0 Level(4,Ally,True) [6,12] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneFortEntrance:
UNIT Lua Captain 0 Level(10,Ally,False) [8,13] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [10,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [9,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Desiderio 0 0 Level(4,Ally,True) [9,13] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneErnieSwoopingIn:
UNIT Khazhak Gryphoknight 0 Level(10,NPC,True) [9,2] 0 0 0 0 [SilverLance, Pike, Javelin] NoAI
UNIT

IntroCutsceneFortNight:
UNIT Lua Captain 0 Level(10,Ally,False) [15,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [28,6] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [21,4] 0x0 0x0 0 0 [0x0] NoAI
UNIT Desiderio 0 0 Level(4,Ally,True) [6,8] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneFortDay:
UNIT Desiderio 0 0 Level(4,Ally,True) [16,5] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneLuaArrives:
UNIT Lua Captain 0 Level(10,Ally,False) [26,8] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

IntroCutsceneUnitsGathered:
UNIT Pilar 0 0 Level(1,Ally,True) [4,4] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [5,2] 0x0 0x0 0 0 [0x0] NoAI
UNIT Khazhak Gryphoknight 0 Level(11,NPC,True) [7,3] 0 0 0 0 [SilverLance, Pike, Javelin] NoAI
UNIT

IntroCutsceneLuaAndDesiderio:
UNIT Lua Captain 0 Level(10,Ally,False) [6,0] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(4,Ally,True) [5,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT

BeginningScene:
//Village entrance; they arrive there, about to head north to the fort.
FADI 99
TILECHANGE TC_B3CutsceneVillage
CAMERA_CENTERED [0,0]
MUSC WindGustSoundID
FADU 12
BROWNBOX HubB_3TownName [0,0]
STAL 60
LoadCutsceneUnits(IntroCutsceneVillage)
MOVE 3 Desiderio [6,4]
STAL 15
MOVE 3 Lua [7,5]
STAL 30
MOVE 3 Pilar [5,5]
STAL 20
MOVE 3 Razvan [6,6]
ENUN
FlashCursor(Desiderio, 60)
Text(HubB3BeginningScene1_1)

//They walk off.
STAL 10
MOVE 6 Desiderio [9,0]
STAL 15
MOVE 6 Lua [8,0]
MOVE 6 Pilar [8,1]
MOVE 6 Razvan [9,1]
FADI 10
ENUN
CLEA 0

//In front of the fort now. 
CAMERA_CENTERED [0,7]
STAL 60
TILEREVERT TC_B3CutsceneVillage
TILECHANGE TC_B3CutsceneFortOutside
LoadCutsceneUnits(IntroCutsceneFortEntrance)
STAL 10
FADU 10
MOVE 4 Lua [8,9]
MOVE 4 Desiderio [9,8]
STAL 40
MOVE 4 Pilar [10,9]
MOVE 4 Razvan [9,10]
ENUN
STAL 60
//outside a ruined fort
MUSC 0x7FFF
Text(HubB3BeginningScene1_2)

//Ernest appears, but isn't quite hostile!
LoadCutsceneUnits(IntroCutsceneErnieSwoopingIn)
MOVE 6 Khazhak [9,7]
MUSC StillZephyrsID
ENUN
FlashCursor(Khazhak, 60)
Text(HubB3BeginningScene1_3A)

//Others move up.

MOVE 6 Pilar [10,1]
MOVE 6 Razvan [9,1]
MOVE 6 Desiderio [9,0]
MOVE 6 Khazhak [10,0]
ENUN
STAL 30
FlashCursor(Lua, 60)

Text(HubB3BeginningScene1_3B)
FADI 10
CLEA 0
CLEE 0
CLEN 0

//Time skipping to a new area.
STAL 60
MUSC NighttimeSoundID
TILEREVERT TC_B3CutsceneFortOutside
TILECHANGE TC_B3CutsceneFortInterior
LoadCutsceneUnits(IntroCutsceneFortNight)
CAMERA_CENTERED [21,8]
FADU 3
STAL 60
FlashCursor(Razvan, 60)
Text(HubB3BeginningScene2_1A)
STAL 20

MOVE 2 Razvan [20,5]
ENUN
STAL 30
Text(HubB3BeginningScene2_1B)
MOVE 3 Razvan [16,9]
ENUN
STAL 30
CameraCursor(Desiderio)
MUSC 0x7FFF
Text(HubB3BeginningScene2_2)

//Razvan approaches Desiderio.
MOVE 4 Razvan [7,9]
STAL 85
WEATHER 1
ENUN
STAL 30
FlashCursor(Razvan, 60)
MUSC CastawayHeartID                    
Text(HubB3BeginningScene2_3A)

//Camera pans to the graves of the fallen Roseblades. 
CAMERA_CENTERED [5,2]
STAL 60
MOVE 2 Razvan [7,8]
ENUN
STAL 30
Text(HubB3BeginningScene2_3B)

//Razvan runs off. 
MOVE 4 Razvan [20,9]
ENUN
STAL 60
Text(HubB3BeginningScene2_3C)

STAL 10
MOVE 3 Desiderio [5,4]
ENUN
FlashCursor(5, 2, 60)
Text(HubB3BeginningScene2_3D)
STAL 30

FADI 3
CLEN 0
CLEA 0
MUSC 0x7FFF
STAL 30
WEATHER 0 

LoadCutsceneUnits(IntroCutsceneFortDay) //desiderio in his room alone 
CAMERA_CENTERED Desiderio
SetBackground(0x35)
FADU 3
CenterTextbox(HubB3BeginningScene3_NarratorMessage)
EraseBackground
STAL 40
FlashCursor(Desiderio, 60)
Text(HubB3BeginningScene3_1A)

MUSC SwabbieTalkID
LoadCutsceneUnits(IntroCutsceneLuaArrives)
MOVE 10 Lua [16,7]
ENUN
FlashCursor(Lua, 60)
Text(HubB3BeginningScene3_1B)
STAL 30
FADI 10
STAL 30

TILEREVERT TC_B3CutsceneFortInterior
CAMERA_CENTERED [0,0]
CLEN 0
LoadCutsceneUnits(IntroCutsceneUnitsGathered)
LoadEnemies(EnemyUnits)
ENUN
FADU 3
STAL 50
LoadCutsceneUnits(IntroCutsceneLuaAndDesiderio)
MOVE 3 Lua [6,2]
MOVE 3 Desiderio [5,1]
ENUN 
FlashCursor(Lua, 60)

SetBackground(0xA) //inner castle area
MUSC FinInTheCloudsID
Text(HubB3BeginningScene3_2)
EraseBackground
STAL 10
CUSA Khazhak
STAL 40
FADI 10
REMOVEUNIT Khazhak
STAL 5
RemoveAll
LoadPlayerUnits(NewUnits)
LoadEnemies(EnemyUnits)
GotoPrepScreen
NoFade 
ENDA

EndingScene:
FADI 15
RemoveAll //remove all the units
RemoveTrapAtCoords(28,0)
LoadCutsceneUnits(EndingCutsceneGuys)
ENUN 
CAMERA_CENTERED Azahar
SVAL s1 1
SET_HP Azahar
STAL 30
FADU 15
FlashCursor(Azahar, 30)
MUSC SwabbieTalkID
CHECK_ALIVE Ernest
BEQ 776 s0 sC //if ernest is dead, skip the rest
    CHECK_EVENTID RebelThiefDeathQuoteFlag
    BNE 777 s0 sC
        TEXTSTART
        TEXTSHOW B3EndingScene2ThievesSpared
        TEXTEND
        MOVE 15 Azahar [28,0]
        ENUN
        REMOVEUNIT Azahar
        TEXTCONT
        TEXTEND
        REMA
        GiveItem(Thorn, Desiderio)
        GOTO 778
    LABEL 777
        TEXTSTART
        TEXTSHOW B3EndingScene2ThievesNotSpared
        TEXTEND
        MOVE 15 Azahar [28,0]
        ENUN
        REMOVEUNIT Azahar
        TEXTCONT
        TEXTEND
        REMA
        GOTO 778
LABEL 776
Text(B3EndingScene2ErnestDead_1)
MOVE 15 Desiderio [30,2]
ENUN
StartBattle
NormalDamage(0, 39)
EndAttack
SetBattleNumbers(90,30,0,80,10,0)
FIGHT_MAP Desiderio Azahar 0x00 0x00
STAL 30
FlashCursor(Desiderio,30)
TEXTSTART
TEXTSHOW B3EndingScene2ErnestDead_2
TEXTEND
MOVE 15 Desiderio [28,0]
ENUN
HIDEUNIT Desiderio
TEXTCONT
TEXTEND
REMA
LABEL 778
FADI 10
SHOWUNIT Desiderio
FadeoutMusic
STAL 30
SetBackground(0x8) //Boat
CALL GiveSuppliesItemsCheck
CHECK_ALIVE Ernest
BEQ 781 s0 sC
    MUSC(CastawayHeartID)
    Text(B3EndingScene3_2_ErnestAlive)
    FADI 15
    STAL 60
    SetBackground(0x8)
    Text(B3EndingScene4ErnestAlive)
    GOTO 782
LABEL 781
MUSC CherishTheDeadID //sad music, putting this here for now
Text(B3EndingScene3_2_ErnestDead)
Text(B3EndingScene4ErnestDead)
LABEL 782
FADI 15
STAL 60
GoToHubChapter(HubB)
NoFade
ENDA

GiveSuppliesItemsCheck:
GetNumberOfSuppliesItemsOnHand
SVAL s1 6
BLT 1 sC s1 //if number of supplies items is less than 7, do not play all fuel thing
    Text(B3EndingScene3_1_AllFuel)
    GiveItemWithDurability(SkillScroll,KeenFighterID,Lua)
    GiveItem(Tomahawk,Lua)
    GiveItem(Bloodletter,Lua)
    GiveItem(GlassShield,Lua)
    GOTO 5
LABEL 1
Text(B3EndingScene3_1_SomeFuel)
GetNumberOfSuppliesItemsOnHand
SVAL s1 4
BLT 3 sC s1 //check sC's amount, give items accordingly
    GiveItem(Tomahawk,Lua)
    GiveItem(Bloodletter,Lua)
    GiveItem(GlassShield,Lua)
    GOTO 5
LABEL 3
GetNumberOfSuppliesItemsOnHand
SVAL s1 2
BLT 4 sC s1
    GiveItem(Bloodletter,Lua)
    GiveItem(GlassShield,Lua)
    GOTO 5
LABEL 4
GiveItem(GlassShield,Lua) //the pity reward for 1 or 0 supplies
LABEL 5
RemoveAllItem(SuppliesItem)
NoFade
ENDA

EndingCutsceneGuys:
UNIT Lua Captain 0 Level(10,Ally,False) [27,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio 0 0 Level(4,Ally,True) [29,2] 0x0 0x0 0 0 [0x0] NoAI
UNIT Azahar Champion_F 0 Level(12, Enemy, True) [28,1] 0 0 0 0 [Spear, SilverSword] BossAI
UNIT



Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN