//Tile change IDS
#define TC_LeftshipFloor1 0x0
#define TC_MidshipFloor1 0x1
#define TC_MidshipFloor2 0x2
#define TC_MidshipFloor3 0x3
#define TC_RightshipFloor1 0x4
#define TC_RightshipFloor2 0x5



//7-14: all used already (here or in outskirts map)
#define Turn1EventFlag 12

// i can probably just use the other temp flags down here
#define HubBJaimeLeonorTempTalkFlag 2

//15-19: hub b talk convo flags
#define HubBDesiTempTalkFlag 15
#define HubBOrlandoTempTalkFlag 16
#define HubBNeltumeTempTalkFlag 17
#define HubBTrianaTempTalkFlag 18
#define HubBMagdalenaTempTalkFlag 19
#define HubBEstelleIndioTempTalkFlag 20
#define HubBPilarTempTalkFlag 21
#define HubBJuanTempTalkFlag 22
#define HubBOndehBikoTempTalkFlag 23
#define HubBAdrianLexaTempTalkFlag 24
#define HubBHermenegildoPonteTempTalkFlag 25
#define HubBNoahTempTalkFlag 26
#define HubBMedwynTempTalkFlag 27
#define HubBCarmenFataTempTalkFlag 29
#define HubBBartzTempTalkFlag 30
#define HubBAvocadoTempTalkFlag 31
#define HubBPassarolaXimoTempTalkFlag 32
#define HubBHusniyeTempTalkFlag 33
#define HubBRazvanRahelTempTalkFlag 34
#define HubBSuzanneMatteoTangyuanTempTalkFlag 35
#define HubBYodsenDmitrTempTalkFlag 36


//hub maneuvering flags (37-40)
#define Flag_Midship_ToDeck 37
#define Flag_MidShip_Floor1ToDeck Flag_Midship_ToDeck
#define Flag_MidShip_Floor2ToDeck Flag_Midship_ToDeck
#define Flag_MidShip_Floor3ToDeck Flag_Midship_ToDeck
#define Flag_Midship_ToFloor1 38
#define Flag_MidShip_DeckToFloor1 Flag_Midship_ToFloor1
#define Flag_MidShip_Floor2ToFloor1 Flag_Midship_ToFloor1
#define Flag_MidShip_ToFloor2 39
#define Flag_MidShip_Floor1ToFloor2 Flag_MidShip_ToFloor2
#define Flag_MidShip_Floor3ToFloor2 Flag_MidShip_Floor1ToFloor2
#define Flag_MidShip_ToFloor3 40
#define Flag_MidShip_Floor2ToFloor3 Flag_MidShip_ToFloor3

//I don't think different flags are needed for each way you could go to a floor, so trying to cut down here and save some temp flags - Loog

EventPointerTable(HubBEventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(Turn1EventFlag, Turn1Event, 1)
END_MAIN

Turn1Event:
FADI 15
CAMERA_CENTERED Lua
CALL SpawnDeckUnits
CALL Traps_Deck
CALL SetAllHubBTalkFlags
ENUT Flag_Midship_ToDeck
ENUT Flag_MidShip_ToFloor2
ENUT Flag_MidShip_ToFloor3 //turn off all flags except the ones that go to floor 1
ENUF Flag_Midship_ToFloor1
FADU 15
CheckHowManyHubChaptersCompleted
SVAL s1 0 
BNE 2 sC s1 //if chapters completed != 5, skip
    CenterTextbox(HubBArmadaExplanation) //initial explanation
LABEL 2
SVAL s1 5
BNE 3 sC s1
    CenterTextbox(HubBSayWhereToGo) //note that you can move on
LABEL 3
NoFade
ENDA

//left ship deck: Desiderio, Ernest, Leandra
//mid ship deck: Jaime, Leonor, Neltume, Triana
//right ship deck: Orlando, Durgham, Magdalena

//mid ship floor 1: Passarola/Ximo, Husniye
//mid ship floor 2 (stable): Razvan/Rahel, Suzanne/Matteo/Tangyuan
//mid ship floor 3: Estelle/Indio, Yodsen/Dmitr

//right ship floor 1 (mess hall?): Biko/Ondeh, Adrian/Lexa, Hermenegildo/Ponte, Medwyn, Noah
//right ship floor 2 (treasure room): Pilar/Kai, Juan

SetAllHubBTalkFlags: // logic here: 
// I need to check to make sure all the characters are present: if not, set the temp talk flag so that the talk can't take place
// should just be check_alive for whoever's there
// I also need to make sure the talk hasn't already happened, which means i need to check a global flag for each of the talks
// if the global flag is set, then set the temp talk flag as well so that the talk can't take place twice

//Top Deck Checks
CheckIfHubBTalkLegalThreeChar(Desiderio, Ernest, Leandra, 1, 2, HubBDesiGlobalTalkFlag, HubBDesiTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Orlando, Durgham, 3, 4, HubBOrlandoGlobalTalkFlag, HubBOrlandoTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Neltume, 5, 6, HubBNeltumeGlobalTalkFlag, HubBNeltumeTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Triana, 7, 8, HubBTrianaGlobalTalkFlag, HubBTrianaTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Magdalena, 9, 10, HubBMagdalenaGlobalTalkFlag, HubBMagdalenaTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Jaime, Leonor, 43, 44, HubBJaimeLeonorGlobalTalkFlag, HubBJaimeLeonorTempTalkFlag)

//Right Ship Floor 2
CheckIfHubBTalkLegalTwoChar(Pilar, Kai, 11, 12, HubBPilarGlobalTalkFlag, HubBPilarTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Juan, 13, 14, HubBJuanGlobalTalkFlag, HubBJuanTempTalkFlag)

//Right Ship Floor 1
CheckIfHubBTalkLegalTwoChar(Ondeh, Biko, 15, 16, HubBOndehBikoGlobalTalkFlag, HubBOndehBikoTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Adrian, Lexa, 17, 18, HubBAdrianLexaGlobalTalkFlag, HubBAdrianLexaTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Hermenegildo, Ponte, 19, 20, HubBHermenegildoPonteGlobalTalkFlag, HubBHermenegildoPonteTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Noah, 21, 22, HubBNoahGlobalTalkFlag, HubBNoahTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Medwyn, 23, 24, HubBMedwynGlobalTalkFlag, HubBMedwynTempTalkFlag)

//Left Ship Floor 1
CheckIfHubBTalkLegalTwoChar(Carmen, Fata, 25, 26, HubBCarmenFataGlobalTalkFlag, HubBCarmenFataTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Bartz, 27, 28, HubBBartzGlobalTalkFlag, HubBBartzTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Avocado, 29, 30, HubBAvocadoGlobalTalkFlag, HubBAvocadoTempTalkFlag)

//Midship Floor 1
CheckIfHubBTalkLegalTwoChar(Passarola, Ximo, 31, 32, HubBPassarolaXimoGlobalTalkFlag, HubBPassarolaXimoTempTalkFlag)
CheckIfHubBTalkLegalOneChar(Husniye, 33, 34, HubBHusniyeGlobalTalkFlag, HubBHusniyeTempTalkFlag)

//Midship Floor 2
CheckIfHubBTalkLegalThreeChar(Suzanne, Matteo, Tangyuan, 35, 36, HubBSuzanneMatteoTangyuanGlobalTalkFlag, HubBSuzanneMatteoTangyuanTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Razvan, Rahel, 37, 38, HubBRazvanRahelGlobalTalkFlag, HubBRazvanRahelTempTalkFlag)

//Midship Floor 3
CheckIfHubBTalkLegalTwoChar(Estelle, Indio, 39, 40, HubBEstelleIndioGlobalTalkFlag, HubBEstelleIndioTempTalkFlag)
CheckIfHubBTalkLegalTwoChar(Yodsen, Dmitr, 41, 42, HubBYodsenDmitrGlobalTalkFlag, HubBYodsenDmitrTempTalkFlag)
NoFade
ENDA


CharacterBasedEvents:
//top deck
CharacterEvent(HubBDesiTempTalkFlag, HubBDesiderioErnestLeandraConvo, Lua, Desiderio)
CharacterEvent(HubBDesiTempTalkFlag, HubBDesiderioErnestLeandraConvo, Lua, Ernest)
CharacterEvent(HubBDesiTempTalkFlag, HubBDesiderioErnestLeandraConvo, Lua, Leandra)

CharacterEvent(HubBOrlandoTempTalkFlag, HubBOrlandoDurghamConvo, Lua, Orlando)
CharacterEvent(HubBOrlandoTempTalkFlag, HubBOrlandoDurghamConvo, Lua, Durgham)

CharacterEvent(HubBNeltumeTempTalkFlag, HubBNeltumeConvo, Lua, Neltume)

CharacterEvent(HubBTrianaTempTalkFlag, HubBTrianaConvo, Lua, Triana)

CharacterEvent(HubBMagdalenaTempTalkFlag, HubBMagdalenaConvo, Lua, Magdalena)

//right ship
CharacterEvent(HubBPilarTempTalkFlag, HubBPilarConvo, Lua, Pilar)
CharacterEvent(HubBPilarTempTalkFlag, HubBPilarConvo, Lua, Kai)

CharacterEvent(HubBJuanTempTalkFlag, HubBJuanConvo, Lua, Juan)

CharacterEvent(HubBOndehBikoTempTalkFlag, HubBOndehBikoConvo, Lua, Ondeh)
CharacterEvent(HubBOndehBikoTempTalkFlag, HubBOndehBikoConvo, Lua, Biko)

CharacterEvent(HubBAdrianLexaTempTalkFlag, HubBAdrianLexaConvo, Lua, Adrian)
CharacterEvent(HubBAdrianLexaTempTalkFlag, HubBAdrianLexaConvo, Lua, Lexa)

CharacterEvent(HubBHermenegildoPonteTempTalkFlag, HubBHermenegildoPonteConvo, Lua, Hermenegildo)
CharacterEvent(HubBHermenegildoPonteTempTalkFlag, HubBHermenegildoPonteConvo, Lua, Ponte)

CharacterEvent(HubBNoahTempTalkFlag, HubBNoahConvo, Lua, Noah)

CharacterEvent(HubBMedwynTempTalkFlag, HubBMedwynConvo, Lua, Medwyn)

//left ship
CharacterEvent(HubBCarmenFataTempTalkFlag, HubBCarmenFataConvo, Lua, Carmen)
CharacterEvent(HubBCarmenFataTempTalkFlag, HubBCarmenFataConvo, Lua, Fata)

CharacterEvent(HubBBartzTempTalkFlag, HubBBartzConvo, Lua, Bartz)

CharacterEvent(HubBAvocadoTempTalkFlag, HubBAvocadoConvo, Lua, Avocado)

//middle ship
CharacterEvent(HubBPassarolaXimoTempTalkFlag, HubBPassarolaXimoConvo, Lua, Passarola)
CharacterEvent(HubBPassarolaXimoTempTalkFlag, HubBPassarolaXimoConvo, Lua, Ximo)

CharacterEvent(HubBHusniyeTempTalkFlag, HubBHusniyeConvo, Lua, Husniye)

CharacterEvent(HubBRazvanRahelTempTalkFlag, HubBRazvanRahelConvo, Lua, Razvan)
CharacterEvent(HubBRazvanRahelTempTalkFlag, HubBRazvanRahelConvo, Lua, Rahel)

CharacterEvent(HubBSuzanneMatteoTangyuanTempTalkFlag, HubBSuzanneMatteoTangyuanConvo, Lua, Suzanne)
CharacterEvent(HubBSuzanneMatteoTangyuanTempTalkFlag, HubBSuzanneMatteoTangyuanConvo, Lua, Matteo)
CharacterEvent(HubBSuzanneMatteoTangyuanTempTalkFlag, HubBSuzanneMatteoTangyuanConvo, Lua, Tangyuan)

CharacterEvent(HubBEstelleIndioTempTalkFlag, HubBEstelleIndioConvo, Lua, Estelle)
CharacterEvent(HubBEstelleIndioTempTalkFlag, HubBEstelleIndioConvo, Lua, Indio)

CharacterEvent(HubBYodsenDmitrTempTalkFlag, HubBYodsenDmitrConvo, Lua, Yodsen)
CharacterEvent(HubBYodsenDmitrTempTalkFlag, HubBYodsenDmitrConvo, Lua, Dmitr)

CharacterEvent(HubBJaimeLeonorTempTalkFlag, HubBJaimeLeonorConvo, Lua, Jaime)
CharacterEvent(HubBJaimeLeonorTempTalkFlag, HubBJaimeLeonorConvo, Lua, Leonor)

END_MAIN

HubBJaimeLeonorConvo:
DisplayTextThenSetGlobalFlag(HubBLuaJaimeLeonorTalk, HubBJaimeLeonorGlobalTalkFlag)
NoFade
ENDA

HubBDesiderioErnestLeandraConvo:
SET_ACTIVE 0
ENUN
DisplayTextThenSetGlobalFlag(HubBLuaDesiderioErnestLeandraTalk, HubBDesiGlobalTalkFlag)
SET_ACTIVE Lua
NoFade
ENDA

HubBOrlandoDurghamConvo:
DisplayTextThenSetGlobalFlag(HubBLuaOrlandoDurghamTalk, HubBOrlandoGlobalTalkFlag) 
NoFade
ENDA

HubBNeltumeConvo:
DisplayTextThenSetGlobalFlag(HubBLuaNeltumeTalk, HubBNeltumeGlobalTalkFlag) 
NoFade
ENDA

HubBTrianaConvo:
DisplayTextThenSetGlobalFlag(HubBLuaTrianaTalk, HubBTrianaGlobalTalkFlag) 
NoFade
ENDA

HubBMagdalenaConvo:
DisplayTextThenSetGlobalFlag(HubBLuaMagdalenaTalk, HubBMagdalenaGlobalTalkFlag) 
NoFade
ENDA

HubBPilarConvo:
DisplayTextThenSetGlobalFlag(HubBLuaPilarKaiTalk, HubBPilarGlobalTalkFlag) 
NoFade
ENDA

HubBJuanConvo:
DisplayTextThenSetGlobalFlag(HubBLuaJuanTalk, HubBJuanGlobalTalkFlag) 
NoFade
ENDA

HubBOndehBikoConvo:
DisplayTextThenSetGlobalFlag(HubBLuaOndehBikoTalk, HubBOndehBikoGlobalTalkFlag) 
NoFade
ENDA

HubBAdrianLexaConvo:
DisplayTextThenSetGlobalFlag(HubBLuaAdrianLexaTalk, HubBAdrianLexaGlobalTalkFlag) 
NoFade
ENDA

HubBHermenegildoPonteConvo:
DisplayTextThenSetGlobalFlag(HubBLuaHermenegildoPonteTalk, HubBHermenegildoPonteGlobalTalkFlag) 
NoFade
ENDA

HubBNoahConvo:
DisplayTextThenSetGlobalFlag(HubBLuaNoahTalk, HubBNoahGlobalTalkFlag) 
NoFade
ENDA

HubBMedwynConvo:
DisplayTextThenSetGlobalFlag(HubBLuaMedwynTalk, HubBMedwynGlobalTalkFlag) 
NoFade
ENDA

HubBCarmenFataConvo:
DisplayTextThenSetGlobalFlag(HubBLuaCarmenFataTalk, HubBCarmenFataGlobalTalkFlag) 
NoFade
ENDA

HubBBartzConvo:
DisplayTextThenSetGlobalFlag(HubBLuaBartzTalk, HubBBartzGlobalTalkFlag) 
NoFade
ENDA

HubBAvocadoConvo:
DisplayTextThenSetGlobalFlag(HubBLuaAvocadoTalk, HubBAvocadoGlobalTalkFlag) 
NoFade
ENDA

HubBPassarolaXimoConvo:
DisplayTextThenSetGlobalFlag(HubBLuaPassarolaXimoTalk, HubBPassarolaXimoGlobalTalkFlag) 
NoFade
ENDA

HubBHusniyeConvo:
DisplayTextThenSetGlobalFlag(HubBLuaHusniyeTalk, HubBHusniyeGlobalTalkFlag) 
NoFade
ENDA

HubBRazvanRahelConvo:
DisplayTextThenSetGlobalFlag(HubBLuaRazvanRahelTalk, HubBRazvanRahelGlobalTalkFlag) 
NoFade
ENDA

HubBSuzanneMatteoTangyuanConvo:
SET_ACTIVE 0
DisplayTextThenSetGlobalFlag(HubBLuaSuzanneMatteoTangyuanTalk, HubBSuzanneMatteoTangyuanGlobalTalkFlag)
SET_ACTIVE Lua
NoFade
ENDA

HubBEstelleIndioConvo:
DisplayTextThenSetGlobalFlag(HubBLuaEstelleIndioTalk, HubBEstelleIndioGlobalTalkFlag) 
NoFade
ENDA

HubBYodsenDmitrConvo:
DisplayTextThenSetGlobalFlag(HubBLuaYodsenDmitrTalk, HubBYodsenDmitrGlobalTalkFlag) 
NoFade
ENDA

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
AREA 0 SwitchToFlyingMode [17,5] [17,5]
AREA GoToNextHubFlagID GotoChapter3 [4,18] [4,18]
//Left ship
AREA Flag_Midship_ToFloor1 Travel_LeftShipDeck_ToFloor1 [4,16] [5,16]
AREA Flag_Midship_ToFloor1 Travel_LeftShipDeck_ToFloor1 [2,23] [2,23]
AREA Flag_Midship_ToFloor1 Travel_LeftShipDeck_ToFloor1 [7,23] [7,23]
//Floor1
AREA Flag_Midship_ToDeck Travel_LeftShipFloor1_ToDeck [4,16] [5,16]
AREA Flag_Midship_ToDeck Travel_LeftShipFloor1_ToDeck [2,23] [2,23]
AREA Flag_Midship_ToDeck Travel_LeftShipFloor1_ToDeck [7,23] [7,23]

//Mid ship
AREA Flag_Midship_ToFloor1 Travel_MidshipDeck_ToFloor1 [17,10] [18,10]
AREA Flag_Midship_ToFloor1 Travel_MidshipDeck_ToFloor1 [20,8] [20,8]
AREA Flag_Midship_ToFloor1 Travel_MidshipDeck_ToFloor1 [13,12] [13,12]
//Floor1
AREA Flag_Midship_ToDeck Travel_MidshipFloor1_ToDeck [17,11] [18,12]
AREA Flag_Midship_ToDeck Travel_MidshipFloor1_ToDeck [20,8] [20,8]
AREA Flag_Midship_ToDeck Travel_MidshipFloor1_ToDeck [13,12] [13,12]
AREA Flag_Midship_ToDeck Travel_MidshipFloor1_ToDeck [15,23] [15,23]
AREA Flag_MidShip_ToFloor2 Travel_MidshipFloor1_ToFloor2 [19,11] [20,11]
//Floor2
AREA Flag_Midship_ToFloor1 Travel_MidshipFloor2_ToFloor1 [19,10] [20,11]
AREA Flag_MidShip_ToFloor3 Travel_MidshipFloor2_ToFloor3 [15,22] [16,23]
//Floor3
AREA Flag_MidShip_ToFloor2 Travel_MidshipFloor3_ToFloor2 [15,22] [16,23]
AREA Flag_Midship_ToDeck Travel_MidshipFloor3_ToDeck [20,8] [20,8]

//Right ship
AREA Flag_Midship_ToFloor1 Travel_RightShipDeck_ToFloor1 [27,17] [27,17]
AREA Flag_Midship_ToFloor1 Travel_RightShipDeck_ToFloor1 [32,17] [32,17]
AREA Flag_Midship_ToFloor1 Travel_RightShipDeck_ToFloor1 [33,12] [33,12]
//Floor1
AREA Flag_Midship_ToDeck Travel_RightShipFloor1_ToDeck [27,17] [27,17]
AREA Flag_Midship_ToDeck Travel_RightShipFloor1_ToDeck [32,17] [32,17]
AREA Flag_Midship_ToDeck Travel_RightShipFloor1_ToDeck [33,12] [33,12]
AREA Flag_MidShip_ToFloor2 Travel_RightShipFloor1_ToFloor2 [29,9] [30,10]
//Floor2 
AREA Flag_Midship_ToFloor1 Travel_RightShipFloor2_ToFloor1 [29,9] [30,10]
AREA Flag_Midship_ToDeck Travel_RightShipFloor2_ToDeck [33,12] [33,12]

AREA DifficultyChangeFlag DifficultyChangeEvent [5,18] [5,18]
AFEV 0 RefreshLua 0
END_MAIN

SwitchToFlyingMode:
FADI 15
SET_ACTIVE 0
STAL 30
RemoveAll
LOMA HubBTheOutskirts
ENUN
LOAD1 1 LiberatorShipGroup
ENUN
LoadNPCUnitGroupIfEIDNotSet(PassarolaRecruitedFlag, 1337, PassarolaUnitGroup)
SET_ACTIVE Liberator
CAMERA_CENTERED Liberator
HIDEUNIT Lua
CALL SpawnInHubBQuests
ENUN
FADU 15
NoFade
ENDA

LiberatorShipGroup:
UNIT Liberator Airship 0 Level(20,Ally,False) [20,14] 0 0 0 0 [0x0] NoAI
// a variety of NPCs who are always around

// B-1: two guys: one by the oasis, one by the ruins
UNIT Explorer Myrmidon_F 0 Level(5,NPC,False) [7,6] 0 0 0 0 [0x0] BossAI
UNIT Incendivis Soldier 0 Level(10,NPC,False) [12,10] 0 0 0 0 [0x0] BossAI

// B-2: one guy by the armory, one guy by the forest in the south
UNIT Armorer Civilian_F_3 0 Level(1,NPC,False) [27,24] 0 0 0 0 [0x0] BossAI
UNIT Rebel Ninja 0 Level(10,NPC,False) [31,32] 0 0 0 0 [0x0] BossAI


// B-3: one guy up fishing by the former ruins of the place, isolated
UNIT Skyfisher Fighter 0 Level(10,NPC,False) [28,4] 0 0 0 0 [0x0] BossAI

// B-4: two guys hanging around with Passarola, Flint and Gerald (not actually playable lol), and a third by the armory/vendor
UNIT Flint Gunner 0 Level(10,NPC,False) [4,26] 0 0 0 0 [0x0] BossAI
UNIT Gerald Gunner 0 Level(1,NPC,False) [4,27] 0 0 0 0 [0x0] BossAI
UNIT A6Pirate Brigand 0 Level(5,NPC,False) [6,21] 0 0 0 0 [0x0] BossAI

// B-5 area: one feller hanging out in the forest
UNIT Woodsman Hunter 0 Level(10,NPC,False) [7,35] 0 0 0 0 [0x0] BossAI
UNIT

PassarolaUnitGroup:
UNIT Passarola Adventurer 0 Level(15,NPC,True) [3,28] 0 0 0 0 [AssistStaff, Rewarp, MiniBow] BossAI
UNIT 

SpawnInHubBQuests:
SpawnQuestSigil(WentToHub1, 1, RedSigilTrapID, 11, 6)
SpawnQuestSigil(WentToHub1, 2, RedSigilTrapID, 12, 6)
SpawnQuestSigil(WentToHub2, 3, RedSigilTrapID, 25, 27)
SpawnQuestSigil(WentToHub2, 4, RedSigilTrapID, 25, 28)
SpawnQuestSigil(WentToHub2, 5, RedSigilTrapID, 25, 29)
SpawnQuestSigil(WentToHub3, 6, RedSigilTrapID, 31, 7)
SpawnQuestSigil(WentToHub3, 7, RedSigilTrapID, 32, 7)
SpawnQuestSigil(WentToHub3, 8, RedSigilTrapID, 33, 7)
SpawnQuestSigil(WentToHub4, 9, RedSigilTrapID, 8, 26)
SpawnQuestSigil(WentToHub4, 10, RedSigilTrapID, 9, 26)
SpawnQuestSigil(WentToHub4, 11, RedSigilTrapID, 10, 26)
SpawnQuestSigil(WentToHub4, 12, RedSigilTrapID, 11, 26)
SpawnQuestSigil(WentToHub4, 13, RedSigilTrapID, 12, 26)
SpawnQuestSigil(WentToHub5, 14, RedSigilTrapID, 11, 34)
SpawnQuestSigil(WentToHub5, 15, RedSigilTrapID, 12, 34)
SpawnQuestSigil(WentToHub5, 16, RedSigilTrapID, 13, 34)
ENUN
NoFade
ENDA

GotoChapter3:
GotoHubChapterIfYesHubB(2, Chapter3)
NoFade
ENDA

EmptyEvent:
NoFade
ENDA

RemoveAllTraps: //clears all traps, disables all hub movement flags
RemoveTrapAtCoords(4,18)
RemoveTrapAtCoords(17,5)
RemoveTrapAtCoords(17,10)
RemoveTrapAtCoords(18,10)
RemoveTrapAtCoords(19,10)
RemoveTrapAtCoords(20,10)
RemoveTrapAtCoords(13,12)
RemoveTrapAtCoords(17,12)
RemoveTrapAtCoords(18,12)
RemoveTrapAtCoords(19,11)
RemoveTrapAtCoords(20,11)
RemoveTrapAtCoords(17,20)
RemoveTrapAtCoords(16,22)
RemoveTrapAtCoords(16,23)
RemoveTrapAtCoords(15,22)
RemoveTrapAtCoords(15,23)
RemoveTrapAtCoords(20,8)
RemoveTrapAtCoords(4,11)
RemoveTrapAtCoords(5,11)
RemoveTrapAtCoords(4,16)
RemoveTrapAtCoords(5,16)
RemoveTrapAtCoords(2,23)
RemoveTrapAtCoords(7,23)
RemoveTrapAtCoords(27,17)
RemoveTrapAtCoords(32,17)
RemoveTrapAtCoords(33,12)
RemoveTrapAtCoords(29,9)
RemoveTrapAtCoords(29,10)
RemoveTrapAtCoords(30,9)
RemoveTrapAtCoords(30,10)
RemoveTrapAtCoords(5,18)
ENUT Flag_Midship_ToDeck
ENUT Flag_MidShip_ToFloor2
ENUT Flag_MidShip_ToFloor3 
ENUT Flag_Midship_ToFloor1
NoFade
ENDA

Travel_LeftShipFloor1_ToDeck:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
ENUT DifficultyChangeFlag // can't set the difficulty anymore
InitiateFloorRevertHubB_Leftship(Traps_Deck)
NoFade
ENDA

Travel_LeftShipDeck_ToFloor1:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF DifficultyChangeFlag //we can set the difficulty here only
InitiateFloorSwapHubB_Leftship(Traps_LeftshipFloor1, TC_LeftshipFloor1, EmptyEvent)
NoFade
ENDA

//returning to deck
Travel_MidshipFloor1_ToDeck:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
InitiateFloorRevertHubB_Midship(Traps_Deck)
NoFade
ENDA

Travel_MidshipDeck_ToFloor1:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF Flag_MidShip_ToFloor2
InitiateFloorSwapHubB_Midship(Traps_MidshipFloor1, TC_MidshipFloor1, SpawnMidshipFloor1Units)
NoFade
ENDA

SpawnMidshipFloor1Units:
LoadPlayerUnits(XimoGroup)
LoadAllyUnitGroupIfAlive(Passarola, 1, PassarolaGroup)
LoadAllyUnitGroupIfAlive(Husniye, 2, HusniyeGroup)
NoFade
ENDA

XimoGroup:
UNIT Ximo Archer 0 Level(1,Ally,False) [14,15] 0 0 0 0 [0x0] NoAI //talks to Kai
UNIT

PassarolaGroup:
UNIT Passarola Adventurer 0 Level(1,Ally,False) [14,16] 0 0 0 0 [0x0] NoAI 
UNIT

HusniyeGroup:
UNIT Husniye Thief 0 Level(1,Ally,False) [19,16] 0 0 0 0 [0x0] NoAI 
UNIT

Travel_MidshipFloor1_ToFloor2:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
ENUF Flag_MidShip_ToFloor3
InitiateFloorSwapHubB_Midship(Traps_MidshipFloor2, TC_MidshipFloor2, LoadMidshipFloor2Units)
NoFade
ENDA

Travel_MidshipFloor2_ToFloor1:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF Flag_MidShip_ToFloor2
InitiateFloorSwapHubB_Midship(Traps_MidshipFloor1, TC_MidshipFloor1, SpawnMidshipFloor1Units) 
NoFade
ENDA

Travel_MidshipFloor2_ToFloor3:
CALL RemoveAllTraps
ENUF Flag_MidShip_ToFloor2
ENUF Flag_Midship_ToDeck
InitiateFloorSwapHubB_Midship(Traps_MidshipFloor3, TC_MidshipFloor3, LoadMidshipFloor3Units)
NoFade
ENDA

LoadMidshipFloor3Units:
LoadPlayerUnits(EstelleIndioGroup)
LoadAllyUnitGroupIfAlive(Yodsen, 1, YodsenGroup)
LoadAllyUnitGroupIfAlive(Dmitr, 2, DmitrGroup)
NoFade
ENDA

EstelleIndioGroup:
UNIT Estelle Troubadour 0 Level(1,Ally,False) [13,10] 0 0 0 0 [0x0] NoAI 
UNIT Indio Shaman 0 Level(1,Ally,False) [13,11] 0 0 0 0 [0x0] NoAI 
UNIT

YodsenGroup:
UNIT Yodsen Gunner 0 Level(1,Ally,False) [19,15] 0 0 0 0 [0x0] NoAI 
UNIT

DmitrGroup:
UNIT Dmitr Brigand 0 Level(1,Ally,False) [18,16] 0 0 0 0 [0x0] NoAI 
UNIT

Travel_MidshipFloor3_ToFloor2:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
ENUF Flag_MidShip_ToFloor3
InitiateFloorSwapHubB_Midship(Traps_MidshipFloor2, TC_MidshipFloor2, LoadMidshipFloor2Units)
NoFade
ENDA

LoadMidshipFloor2Units:
LoadPlayerUnits(RazvanRahelUnitGroup)
LoadPlayerUnits(UnitGroup_MF1_Honses)
NoFade
ENDA

RazvanRahelUnitGroup:
UNIT Razvan Curate 0 Level(1,Ally,False) [15,16] 0 0 0 0 [0x0] NoAI //these 2 will talk with Lua
UNIT Rahel BowCav 0 Level(1,Ally,False) [16,16] 0 0 0 0 [0x0] NoAI
UNIT Suzanne Gunner  0 Level(1,Ally,False) [20,17] 0 0 0 0 [0x0] NoAI //these 3 can have a talk
UNIT Matteo Mercenary  0 Level(1,Ally,False) [20,20] 0 0 0 0 [0x0] NoAI
UNIT Tangyuan DragonRider 0 Level(1,Ally,False) [18,18] 0 0 0 0 [0x0] NoAI
UNIT

DmitrUnitGroup:
UNIT Dmitr Brigand 0 Level(1,Ally,False) [15,6] 0 0 0 0 [0x0] NoAI
UNIT 

Travel_MidshipFloor3_ToDeck:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
InitiateFloorRevertHubB_Midship(Traps_Deck)
NoFade
ENDA

Travel_RightShipDeck_ToFloor1:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF Flag_MidShip_ToFloor2
InitiateFloorSwapHubB_Rightship(Traps_RightshipFloor1, TC_RightshipFloor1, SpawnRightShipFloor1Units)
NoFade
ENDA

Travel_RightShipFloor1_ToDeck:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
InitiateFloorRevertHubB_Rightship(Traps_Deck)
NoFade
ENDA

Travel_RightShipFloor1_ToFloor2:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF Flag_Midship_ToFloor1
InitiateFloorSwapHubB_Rightship(Traps_RightshipFloor2, TC_RightshipFloor2, SpawnRightShipFloor2Units)
NoFade
ENDA

SpawnRightShipFloor2Units:
LoadPlayerUnits(PilarGroup)
LoadAllyUnitGroupIfAlive(Juan, 3, JuanGroup)
LoadAllyUnitGroupIfEIDSetAndCharAlive(WentToHub3, Kai, 1, KaiGroup) //only spawn in kai if you went to B-3
NoFade
ENDA

PilarGroup:
UNIT Pilar Ninja 0 Level(1,Ally,False) [32,18] 0 0 0 0 [0x0] NoAI //talks to Kai
UNIT

JuanGroup:
UNIT Juan Thief 0 Level(1,Ally,False) [26,21] 0 0 0 0 [0x0] NoAI //talks to Lua
UNIT

KaiGroup:
UNIT Kai Ninja 0 Level(1,Ally,False) [33,17] 0 0 0 0 [0x0] NoAI 
UNIT

Travel_RightShipFloor2_ToFloor1:
CALL RemoveAllTraps
ENUF Flag_Midship_ToDeck
ENUF Flag_MidShip_ToFloor2
InitiateFloorSwapHubB_Rightship(Traps_RightshipFloor1, TC_RightshipFloor1, SpawnRightShipFloor1Units)
NoFade
ENDA

SpawnRightShipFloor1Units:
LoadPlayerUnits(RSFloor1Group)
LoadAllyUnitGroupIfAlive(Medwyn, 1, MedwynGroup)
LoadAllyUnitGroupIfEIDSetAndCharAlive(WentToHub4, Adrian, 2, AdrianGroup)
LoadAllyUnitGroupIfEIDSetAndCharAlive(WentToHub4, Lexa, 3, LexaGroup)
LoadAllyUnitGroupIfEIDSetAndCharAlive(WentToHub4, Noah, 4, NoahGroup)
NoFade
ENDA

RSFloor1Group:
UNIT Biko Soldier 0 Level(1,Ally,False) [27,23] 0 0 0 0 [0x0] NoAI
UNIT Ondeh Hunter_F 0 Level(1,Ally,False) [28,23] 0 0 0 0 [0x0] NoAI
UNIT Hermenegildo Myrmidon 0 Level(1,Ally,False) [27,19] 0 0 0 0 [0x0] NoAI
UNIT Ponte Brigand 0 Level(1,Ally,False) [28,19] 0 0 0 0 [0x0] NoAI
UNIT

AdrianGroup:
UNIT Adrian Fighter 0 Level(1,Ally,False) [31,23] 0 0 0 0 [0x0] NoAI
UNIT

LexaGroup:
UNIT Lexa Myrmidon_F 0 Level(1,Ally,False) [32,23] 0 0 0 0 [0x0] NoAI
UNIT

NoahGroup:
UNIT Noah Warden 0 Level(1,Ally,False) [26,12] 0 0 0 0 [0x0] NoAI
UNIT

MedwynGroup:
UNIT Medwyn Warden 0 Level(1,Ally,False) [29,13] 0 0 0 0 [0x0] NoAI
UNIT

Travel_RightShipFloor2_ToDeck:
CALL RemoveAllTraps
ENUF Flag_Midship_ToFloor1
InitiateFloorRevertHubB_Rightship(Traps_Deck)
NoFade
ENDA

Traps_LeftshipFloor1:
AddTrap(BlueSigilTrapID, 4, 16)
AddTrap(BlueSigilTrapID, 5, 16)
AddTrap(BlueSigilTrapID, 2, 23)
AddTrap(BlueSigilTrapID, 7, 23)
CHECK_EVENTID GoToNextHubFlagID
BNE 776 s0 sC //if the flag is set, then skip over this part
    AddTrap(GreenSigilTrapID, 4, 18)
LABEL 776
AddTrap(PurpleSigilTrapID, 5, 18) //where we change difficulty
// //These are unaccessible at first. 
//AddTrap(BlueSigilTrapID, 5, 11) //These are unaccessible at first.
//for some event, I guess? I'll comment it out for now - Loog
NoFade
ENDA

Traps_MidshipFloor1:
//back to dock
AddTrap(BlueSigilTrapID, 17, 12)
AddTrap(BlueSigilTrapID, 18, 12)
AddTrap(BlueSigilTrapID, 20, 8)
AddTrap(BlueSigilTrapID, 13, 12)
//to second floor
AddTrap(BlueSigilTrapID, 19, 11)
AddTrap(BlueSigilTrapID, 20, 11)
//AddTrap(BlueSigilTrapID, 17, 20) - not sure what this is for, so commenting it out for now - Loog
NoFade
ENDA

UnitGroup_MF1_Honses:
UNIT HorseUnit HorseClass 0 Level(10,NPC,False) [13,13] 0 0 0 0 [0x0] NoAI
UNIT HorseUnit HorseClass 0 Level(10,NPC,False) [15,8] 0 0 0 0 [0x0] NoAI
UNIT

Traps_MidshipFloor2:
//To floor 1
AddTrap(BlueSigilTrapID, 19, 10)
AddTrap(BlueSigilTrapID, 20, 10)
//To deck
AddTrap(BlueSigilTrapID, 20, 8)
//To floor 3
AddTrap(BlueSigilTrapID, 16, 22)
AddTrap(BlueSigilTrapID, 16, 23)
NoFade
ENDA

Traps_MidshipFloor3:
//To floor 2
AddTrap(BlueSigilTrapID, 15, 22)
AddTrap(BlueSigilTrapID, 15, 23)
//To deck
AddTrap(BlueSigilTrapID, 20, 8)
NoFade
ENDA

Traps_RightshipFloor1:
AddTrap(BlueSigilTrapID, 27, 17)
AddTrap(BlueSigilTrapID, 32, 17)
AddTrap(BlueSigilTrapID, 33, 12)
AddTrap(BlueSigilTrapID, 30, 9)
AddTrap(BlueSigilTrapID, 30, 10)
NoFade
ENDA

Traps_RightshipFloor2:
AddTrap(BlueSigilTrapID, 29, 9)
AddTrap(BlueSigilTrapID, 29, 10)
AddTrap(BlueSigilTrapID, 33, 12)
NoFade
ENDA

///
/// HUB TRAVEL
///

RefreshLua:
SVAL s1 0x42
UNSET_CONDITION Lua
NoFade
ENDA

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [17,5] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

NewUnits:
UNIT Ponte 0 0 Level(6,Ally,False) [5,22] 0 0 0 0 [Hammer, Hammer] NoAI
UNIT 

BeginningScene:
ENUT DifficultyChangeFlag
WEATHER 7
ENUT LuaInAHubFlag
BranchIfHubNotVisited(1, BeginningSceneFirstTimeInHuB)
STAL 1
ENUT GoToNextHubFlagID
CheckHowManyHubChaptersCompleted
SVAL s1 4
BNE 2 sC s1 //if chapters completed != 5, skip
    ENUF GoToNextHubFlagID
LABEL 2
STAL 1
GotoPrepScreenHub
NoFade
ENDA

QuestGiversGreen:
CUSN Razvan
CUSN Rahel
CUSN Desiderio
CUSN Orlando
CUSN Pilar
NoFade
ENDA

QuestGiversBlue:
CUSA Razvan
CUSA Rahel
CUSA Desiderio
CUSA Orlando
CUSA Pilar
NoFade
ENDA

RazvanUnitGroup:
UNIT Razvan Curate 0 Level(10,Ally,False) [28,18] 0 0 0 0 [0x0] NoAI
UNIT

RahelUnitGroup:
UNIT Rahel BowCav 0 Level(10,Ally,False) [16,11] 0 0 0 0 [0x0] NoAI
UNIT

DesiderioUnitGroup:
UNIT Desiderio Vanguard 0 Level(10,Ally,False) [18,21] 0 0 0 0 [0x0] NoAI
UNIT

OrlandoUnitGroup:
UNIT Orlando Trickster 0 Level(10,Ally,False) [6,17] 0 0 0 0 [0x0] NoAI
UNIT

PilarUnitGroup:
UNIT Pilar Ninja 0 Level(10,Ally,False) [26,21] 0 0 0 0 [0x0] NoAI
UNIT

PortGreagorUnits: //copypasta of docks units in the actual hub
UNIT Docksman 0 0 Level(3, NPC, True) [6,4] 0 0 0 0 [SteelLance] NoAI
UNIT GreagorShirker Fighter 0 Level(3, NPC, True) [12,2] 0 0 0 0 [IronAxe] NoAI
UNIT Docksman Mercenary 0 Level(4, NPC, True) [18,13] 0 0 0 0 [KillingEdge] NoAI
UNIT Ida 0 0 Level(20, NPC, False) [8,26] 0 0 0 0 [SilverAxe] NoAI
UNIT GreagorSailor Fighter 0 Level(3,NPC, True) [8,5] 0 0 0 0 [SlimAxe] NoAI
//UNIT GreagorSmuggler Thief 0 Level(6,NPC, False) [2, 24] 0 0 0 0 [SteelKnife, Tonic, 0xB7, 0x9F] NoAI //JunaFruit and 3000 gold as hex values
UNIT GreagorShipwright VillagerMAdult 0 Level(1,NPC,False) [11,14] 0 0 0 0 [] NoAI
UNIT GreagorCaptain Berserker 0 Level(14,NPC,False) [13,14] 0 0 0 0 [SilverAxe, Tomahawk, Elixir] NoAI
UNIT

LuaDesiderioUnits:
UNIT Lua 0 0 Level(10,Ally,False) [6,31] 0 0 1 REDA6R30 [0x0] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [7,31] 0 0 0 0 [0x0] NoAI
UNIT

RahelUnits:
UNIT Rahel 0 0 Level(10,Ally,False) [7,31] 0 0 1 REDA7R26 [0x0] NoAI
UNIT


BeginningSceneFirstTimeInHuB: //this is what I need to event
SetBackground(Ship)
MUSC StillZephyrsID
Text(HubB_BeginningScene_1_1)
FADI 15
LOMA HubA
CAMERA_CENTERED [4,31]
TILECHANGE 0
STAL 60
LoadCutsceneUnits(PortGreagorUnits)
EraseBackground
STAL 30
SOUN PegasusFlyingSoundID
TILECHANGE 5
STAL 30
LoadCutsceneUnits(LuaDesiderioUnits)
FADI 15
STAL 30
SOUN AttackSnagSoundID
TILECHANGE 6
FADU 15
MOVE 15 Lua [9,27]
MOVE 10 Desiderio [7,27]
ENUN
FlashCursor(Ida,60)
Text(HubB_BeginningScene_1_2_1)
MOVE 10 Desiderio [18,24]
ENUN
Text(HubB_BeginningScene_1_2_2)
FadeOutMusic
MOVE 15 Lua [6,31]
ENUN
Text(HubB_BeginningScene_1_2_3)
STAL 30
LoadCutsceneUnits(RahelUnits)
FADI 15
STAL 60
FADU 15
MUSC CastawayHeartID
Text(HubB_BeginningScene_1_3)
MOVE 15 Rahel [2,31]
ENUN
Text(HubB_BeginningScene_1_4_1)
MOVE 15 Lua [7,26]
ENUN
Text(HubB_BeginningScene_1_4_2)
MOVE 15 Lua [6,30]
ENUN
FADI 15
STAL 30
TILEREVERT 6
TILEREVERT 5
TILEREVERT 0 //have to redo the tilechanges since tilerevert straight up erases tiles
TILECHANGE 0
TILECHANGE 5
SOUN AttackSnagSoundID
FADU 15
STAL 30
MOVE 15 Lua [3,31]
ENUN
Text(HubB_BeginningScene_1_5_1)
HIDEUNIT Lua
STAL 60
Text(HubB_BeginningScene_1_5_2)
MOVE 15 Rahel [6,30]
ENUN
Text(HubB_BeginningScene_1_6)
MOVE 5 Rahel [6,31]
ENUN
FlashCursor(Rahel,60)
HIDEUNIT Rahel
Text(HubB_BeginningScene_1_7)


FADI 15 //Scene change to the crew getting together and deciding on their next moves
FadeOutMusic
RemoveAll
SHOWUNIT Lua
SHOWUNIT Rahel
STAL 120
SetBackground(Inn)
BROWNBOXTEXT HubBDaysLaterText[8,8]
MUSC StillZephyrsID
Text(HubB_BeginningScene_2_1)

TEXTSTART
TEXTSHOW HubB_BeginningScene_2_2
TEXTEND
FadeOutMusic
STAL 60
MUSC OrlandoThemeID
TEXTCONT
TEXTEND
REMA

FADI 15
LOMA HubB
ENUN
TILECHANGE 4
LoadCutsceneUnits(CutscenersArrive)
LOAD1 0 PotentialMen //guys who should be here but might be dead
ENUN
LoadAllyUnitGroupIfAlive(Leandra, 236, LeandraBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Juan, 235, JuanBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Yodsen, 237, YodsenBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Durgham, 238, DurghamBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Triana, 239, TrianaBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Medwyn, 240, MedwynBeginningSceneGroup)
LoadAllyUnitGroupIfAlive(Bartz, 241, BartzBeginningSceneGroup)
CAMERA_CENTERED Lua
STAL 60
EraseBackground

FlashCursor(Lua,60)
Text(HubB_BeginningScene_3_1_1)
FlashCursor(Razvan,60)
Text(HubB_BeginningScene_3_1_2)
FlashCursor(Desiderio,60)
Text(HubB_BeginningScene_3_1_3)
FlashCursor(Pilar,60)
Text(HubB_BeginningScene_3_1_4)
FlashCursor(Lua,60)
Text(HubB_BeginningScene_3_1_5)
FadeOutMusic
FlashCursor(Orlando,60)
MUSC NewHandsOnDeckID
Text(HubB_BeginningScene_3_2_Orlando)
FlashCursor(Rahel,60)
Text(HubB_BeginningScene_3_2_Rahel)
IfAliveHighlightThenDialog(Ximo, HubBXimosDown, 560, 60)
IfAliveHighlightThenDialog(Carmen, HubBCarmensDown, 561, 60)
IfAliveHighlightThenDialog(Hermenegildo, HubBHermensDown, 562, 60)
IfAliveHighlightThenDialog(Estelle, HubBEstellesDown, 563, 60)
IfAliveHighlightThenDialog(Indio, HubBIndiosDown, 564, 60)
IfAliveHighlightThenDialog(Leandra, HubBLeandrasDown, 565, 60)
IfAliveHighlightThenDialog(Jaime, HubBJaimesDown, 566, 60)
IfAliveHighlightThenDialog(Leonor, HubBLeonorsDown, 567, 60)
IfAliveHighlightThenDialog(Ondeh, HubBOndehsDown, 568, 60)
IfAliveHighlightThenDialog(Biko, HubBBikosDown, 569, 60)
IfAliveHighlightThenDialog(Juan, HubBJuansDown, 570, 60)
IfAliveHighlightThenDialog(Tangyuan, HubBTangyuansDown, 571, 60)
IfAliveHighlightThenDialog(Suzanne, HubBSuzannesDown, 572, 60)
IfAliveHighlightThenDialog(Matteo, HubBMatteosDown, 573, 60)
IfAliveHighlightThenDialog(Yodsen, HubBYodsensDown, 574, 60)
IfAliveHighlightThenDialog(Durgham, HubBDurghamsDown, 575, 60)
IfAliveHighlightThenDialog(Triana, HubBTrianasDown, 576, 60)
IfAliveHighlightThenDialog(Medwyn, HubBMedwynsDown, 577, 60)
IfAliveHighlightThenDialog(Bartz, HubBBartzsDown, 578, 60)


FlashCursor(Lua,60)
Text(HubB_BeginningScene_3_3)
FlashCursor(Pilar,60)
Text(HubB_BeginningScene_3_4)
FlashCursor(Razvan,60)
Text(HubB_BeginningScene_3_5)
FlashCursor(Desiderio,60)
Text(HubB_BeginningScene_3_6)
STAL 60
SOUN AudienceCheerSoundID
SoloTextbox(HubB_BeginningScene_3_7, 24, 24)
STAL 60
SOUN AudienceCheerSoundID
SoloTextbox(HubB_BeginningScene_3_7, 104, 200)
STAL 60
SOUN AudienceCheerSoundID
SoloTextboxCenter(HubB_BeginningScene_3_8)
STAL 30
FADI 15
FadeOutMusic
TILEREVERT 4
RemoveAll
LoadPlayerUnits(NewUnits)
STAL 120
SetBackground(Inn)
LocationText(HubB_BeginningScene_4_1, 8, 8)
Text(HubB_BeginningScene_4_2)
FADI 15
//here we unset all hub A flags that will be reused for hub B
ENUF HubBDesiGlobalTalkFlag
ENUF HubBOrlandoGlobalTalkFlag
ENUF HubBNeltumeGlobalTalkFlag
ENUF HubBTrianaGlobalTalkFlag
ENUF HubBMagdalenaGlobalTalkFlag
ENUF HubBPilarGlobalTalkFlag
ENUF HubBJuanGlobalTalkFlag
ENUF HubBOndehBikoGlobalTalkFlag
ENUF HubBAdrianLexaGlobalTalkFlag
ENUF HubBHermenegildoPonteGlobalTalkFlag
ENUF HubBNoahGlobalTalkFlag
ENUF HubBMedwynGlobalTalkFlag
ENUF HubBCarmenFataGlobalTalkFlag
ENUF HubBBartzGlobalTalkFlag
ENUF HubBAvocadoGlobalTalkFlag
ENUF HubBPassarolaXimoGlobalTalkFlag
ENUF HubBHusniyeGlobalTalkFlag
ENUF HubBRazvanRahelGlobalTalkFlag
ENUF HubBSuzanneMatteoTangyuanGlobalTalkFlag
ENUF HubBEstelleIndioGlobalTalkFlag
ENUF HubBYodsenDmitrGlobalTalkFlag
NoFade
ENDA

CutscenersArrive:
UNIT Lua 0 0 Level(10,Ally,False) [29,21] 0 0 0 0 [0x0] NoAI
UNIT Desiderio 0 0 Level(10,Ally,False) [31,20] 0 0 0 0 [0x0] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [31,22] 0 0 0 0 [0x0] NoAI
UNIT Razvan Curate 0 Level(1,Ally,False) [30,23] 0 0 0 0 [0x0] NoAI //these 2 will talk with Lua
UNIT Rahel BowCav 0 Level(1,Ally,False) [30,18] 0 0 0 0 [0x0] NoAI
UNIT Orlando Trickster 0 Level(10,Ally,False) [29,17] 0 0 0 0 [0x0] NoAI
UNIT

PotentialMen:
UNIT Carmen 0 0 Level(10,Ally,False) [27,23] 0 0 0 0 [0x0] NoAI
UNIT Ximo 0 0 Level(10,Ally,False) [28,23] 0 0 0 0 [0x0] NoAI
UNIT Hermenegildo 0 0 Level(10,Ally,False) [26,19] 0 0 0 0 [0x0] NoAI
UNIT Estelle 0 0 Level(10,Ally,False) [29,24] 0 0 0 0 [0x0] NoAI
UNIT Indio 0 0 Level(10,Ally,False) [29,17] 0 0 0 0 [0x0] NoAI
UNIT Jaime 0 0 Level(10,Ally,False) [33,21] 0 0 0 0 [0x0] NoAI
UNIT Leonor 0 0 Level(10,Ally,False) [33,22] 0 0 0 0 [0x0] NoAI
UNIT Ondeh 0 0 Level(10,Ally,False) [31,23] 0 0 0 0 [0x0] NoAI
UNIT Biko 0 0 Level(10,Ally,False) [32,23] 0 0 0 0 [0x0] NoAI
UNIT Tangyuan 0 0 Level(10,Ally,False) [27,17] 0 0 0 0 [0x0] NoAI
UNIT Matteo 0 0 Level(10,Ally,False) [27,19] 0 0 0 0 [0x0] NoAI
UNIT Suzanne 0 0 Level(10,Ally,False) [28,19] 0 0 0 0 [0x0] NoAI
UNIT

YodsenBeginningSceneGroup:
UNIT Yodsen 0 0 Level(10,Ally,False) [30,20] 0 0 0 0 [0x0] NoAI
UNIT

DurghamBeginningSceneGroup:
UNIT Durgham 0 0 Level(10,Ally,False) [32,17] 0 0 0 0 [0x0] NoAI
UNIT

TrianaBeginningSceneGroup:
UNIT Triana 0 0 Level(10,Ally,False) [27,20] 0 0 0 0 [0x0] NoAI
UNIT

BartzBeginningSceneGroup:
UNIT Bartz 0 0 Level(10,Ally,False) [27,20] 0 0 0 0 [0x0] NoAI
UNIT

MedwynBeginningSceneGroup:
UNIT Medwyn 0 0 Level(10,Ally,False) [27,20] 0 0 0 0 [0x0] NoAI
UNIT

LeandraBeginningSceneGroup:
UNIT Leandra 0 0 Level(10,Ally,False) [26,21] 0 0 0 0 [0x0] NoAI
UNIT

JuanBeginningSceneGroup:
UNIT Juan 0 0 Level(10,Ally,False) [26,22] 0 0 0 0 [0x0] NoAI
UNIT

EndingScene:
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN