EventPointerTable(HubC_2EventsID, PointerList)

//Some AI helpers for stuff going on in this chapter...
#define WaitOneTurnAI [0x0, 0x12, 0x4, 0x0]
#define ChargeAIRecovery1 [0x0, 0x0, 0x1, 0x0]


PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, Turn1_Explanation, 1)
TurnEventPlayer(0, Turn3_Reinforcements, 3)
TurnEventPlayer(0, Turn5_BridgeLowered, 5)
TurnEventPlayer(0, Turn8_SpawnGriffons, 8)
TurnEventPlayer(0, Turn11_AggroBoss, 11)
TurnEventPlayer(0, EndingScene,1,255)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(9, CalistaRecruitment, 0x0, Calista) //anyone can talk with calista
END_MAIN

LocationBasedEvents:
Chest(BraveBow, 21, 9)
Village(20,QuickdrawVillage,13,24)
END_MAIN

QuickdrawVillage:
//SetBackground(HouseBG)
//Text(A2Village)
//EraseBackground
GiveItem(Quickdraw,0xFFFF)
NoFade
ENDA

MiscBasedEvents:
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Units1:
//Middle Units
UNIT Lua Captain 0 Level(10,Ally,False) [9,26] 0 0 0 0 [IronSword] NoAI
//Valerie
UNIT Valerie Archer 0 Level(4,Ally,True) [7,26] 0 0 0 0 [SteelBow, Snapshot, Tonic] NoAI
LoadUnit(11,26)
LoadUnit(8,27)
LoadUnit(10,27)
LoadUnit(12,27)
LoadUnit(9,28)
LoadUnit(11,28)

//Left side units
LoadUnit(1,26)
LoadUnit(3,26)
LoadUnit(1,27)
LoadUnit(2,27)
LoadUnit(2,28)
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

EnemiesAttackingCalista:
//First wave attacking the other captain

//This dude gets popped lmao, but his stats need to be nerfed because he's too good! 
UNIT BerserkerThatDiesC2 Berserker 0 Level(18,Enemy,True) [5,5] 0 0 0 0 [BraveAxe, BattleAxe, Swordreaver, Eyepatch] ChargeAI
UNIT BlackheartPlus Archer 0 Level(16,Enemy,True) [10,6] 0 0 0 0 [SteelBow] ChargeAI
UNIT BlackheartPlus Vanguard 0 Level(15,Enemy,True) [7,4] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [7,2] 0 0 0 0 [SteelAxe, HandAxe] WaitOneTurnAI

MainAssault:
//Fort units, left to right
UNIT BlackheartPlus Shaman 0 Level(16,Enemy,True) [8,21] 0 0 0 0 [Sagittae] GroupOneAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [11,21] 0 0 0 0 [SteelKnife] AttackInRangeAI
//Left Units
UNIT BlackheartPlus Warrior 0 Level(18,Enemy,True) [0,15] 0 0 0 0 [SteelAxe] GroupOneAI
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [1,19] 0 0 0 0 [SteelAxe, HandAxe] GroupOneAI
UNIT BlackheartPlus Hunter 0 Level(16,Enemy,True) [3,21] 0 0 0 0 [Longbow] GroupOneAI
UNIT BlackheartPlus Champion 0 Level(17,Enemy,True) [2,24] 0 0 0 0 [Javelin, SilverLance, Vulnerary] GroupOneAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [7,15] 0 0 0 0 [SteelAxe, Vulnerary] ChargeAI
//Middle Units
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [5,17] 0 0 0 0 [ArcaneKnife] ChargeAI 
UNIT BlackheartPlus Hero 0 Level(17,Enemy,True) [9,23] 0 0 0 0 [SilverSword, Elixir] ChargeAI
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [10,22] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus Vanguard 0 Level(16,Enemy,True) [9,18] 0 0 0 0 [HandAxe, Vulnerary] ChargeAI
//Right Units
UNIT BlackheartElite Sniper 0 Level(18,Enemy,True) [16,17] 0 0 0 0 [SilverBow] ChargeAI
UNIT BlackheartPlus Deserter 0 Level(16,Enemy,True) [15,18] 0 0 0 0 [SteelLance] ChargeAI
UNIT BlackheartPlus Mercenary 0 Level(16,Enemy,True) [17,19] 0 0 0 0 [SteelSword] ChargeAI
UNIT BlackheartPlus Berserker 0 Level(17,Enemy,True) [15,15] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus Shaman 0 Level(16,Enemy,True) [15,12] 0 0 0 0 [Luna, SecretBook] WaitOneTurnAI
UNIT BlackheartPlus Gunner 0 Level(16,Enemy,True) [16,11] 0 0 0 0 [Blunderbuss] WaitOneTurnAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [19,12] 0 0 0 0 [Tomahawk, TealGem] WaitOneTurnAI
UNIT 

MiddleUnits:
UNIT BlackheartPlus WarMonk 0 Level(17,Enemy,True) [9,14] 0 0 0 0 [IronAxe, Silence] BossAI
UNIT BlackheartPlus WarMonk 0 Level(17,Enemy,True) [10,14] 0 0 0 0 [HandAxe, Physic] [0xE, 0x3, 0x4, 0x20]
UNIT BlackheartPlus Sniper 0 Level(16,Enemy,True) [11,13] 0 0 0 0 [Greatbow, MiniBow] BossAI
UNIT BlackheartPlus Deserter 0 Level(16,Enemy,True) [8,13] 0 0 0 0 [Impaler, Javelin] WaitOneTurnAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [12,10] 0 0 0 0 [SilverDagger] ChargeAI 

//Right middle, protecting the chests
UNIT BlackheartPlus Gunner 1 Level(17,Enemy,True) [21,9] 0 0 0 0 [Culverin, InfiltratorGun] GroupTwoAI
UNIT BlackheartPlus Hunter 1 Level(15,Enemy,True) [20,7] 0 0 0 0 [Longbow, Vulnerary] GroupTwoAI
UNIT BlackheartPlus Hero_F 1 Level(18,Enemy,True) [21,12] 0 0 0 0 [SilverSword] WaitOneTurnAI

//First left stair formation
UNIT BlackheartPlus Marksman 0 Level(18,Enemy,True) [4,14] 0 0 0 0 [Pistol, Pistol, HideShield] GroupFourAI
UNIT BlackheartPlus Archer 0 Level(16,Enemy,True) [5,13] 0 0 0 0 [SteelBow] GroupFourAI //3-13 archer is back!!
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [3,13] 0 0 0 0 [Hammer, Elixir] GroupFourAI

//Second left stair formation, holding the stairs close to captain, but not attacking Calista

UNIT BlackheartPlus Trickster 1 Level(17,Enemy,True) [7,9] 0 0 0 0 [Elfire, IronKnife] GroupThreeAI
UNIT BlackheartPlus Mercenary 1 Level(16,Enemy,True) [6,8] 1 0 0 0 [SilverSword] GroupThreeAI
UNIT BlackheartPlus Deserter 1 Level(16,Enemy,True) [8,8] 0 0 0 0 [Spear] GroupThreeAI
UNIT

RearUnits:

UNIT BlackheartElite Sniper 0 Level(18, Enemy, True) [12,3] 0 0 0 0 [Greatbow] AttackInRangeAI
UNIT BlackheartPlus Warrior 0 Level(18, Enemy, True) [8,1] 0 0 0 0 [SilverAxe, IronBow] AttackInRangeAI

//Boss group

UNIT Salvador 0 0 Level(20,Enemy,True) [18,1] 0 0 0 0 [KillingEdge, KillerLance, FliersPermit] AttackInRangeAI
UNIT BlackheartPlus Assassin 1 Level(19,Enemy,True) [17,0] 0 0 0 0 [SteelKnife, Stiletto] AttackInRangeAI
UNIT BlackheartPlus Marksman 0 Level(19,Enemy,True) [19,2] 0 0 0 0 [InfiltratorGun] AttackInRangeAI

//These guys are going to go charge on Turn 4, when the bridge gets dropped
UNIT BlackheartPlus Fighter 0 Level(16, Enemy, True) [14,5] 0 0 0 0 [HandAxe, Tonic] AttackInRangeAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [17,6] 0 0 0 0 [SteelKnife, SteelDagger] AttackInRangeAI
UNIT BlackheartPlus Hero 0 Level(18,Enemy,True) [16,5] 0 0 0 0 [SteelSword, HandAxe, Elixir] AttackInRangeAI
UNIT

Reinforcements_T3:
//Small trickle of units to attack Calista
UNIT BlackheartPlus Gunner 0 Level(16,Enemy,True) [14,0] 0 0 1 REDA12R0 [SteelMusket] ChargeAI
UNIT BlackheartPlus Mercenary_F 0 Level(16,Enemy,True) [15,0] 0 0 1 REDA13R0 [SteelSword] ChargeAI
UNIT

Reinforcements_T5:
//Reinforcements to help aid bridge attack
UNIT BlackheartElite Sniper 0 Level(18,Enemy,True) [21,6] 0 0 1 REDA19R6 [SilverBow] ChargeAI
UNIT BlackheartPlus Mercenary 0 Level(16,Enemy,True) [21,5] 0 0 1 REDA19R5 [Onigiri] ChargeAI
UNIT

//Meant to kill the imperial captain if you aren't quick enough! 
GriffonReinforcements:
UNIT TorchUnit Gryphoknight 0 Level(18,Enemy,True) [3,0] 0 0 0 0 [Impaler] ChargeAI
UNIT TorchUnit Gryphoknight 0 Level(18,Enemy,True) [4,0] 0 0 0 0 [SteelLance, Horseslayer] ChargeAI
UNIT

CalistaTrapped:
UNIT Calista Dragoon 0 Level(18,NPC,True) [0,5] 0 0 0 0 [Toradar, SteelSword, Elixir, Mine] BossAI
UNIT

SmallRebelGarrison:
UNIT KeeperAlly Halberdier 0 Level(17,NPC,True) [17,24] 0 0 0 0 [SilverLance, Axereaver, Elixir] ChargeAIRecovery1 //ChargeAI, but with recovery mode 30% HP. 
UNIT KeeperAlly Mercenary 0 Level(16,NPC,True) [15,24] 0 0 0 0 [SteelSword, Wingclipper, IronShield, Vulnerary] ChargeAIRecovery1
UNIT KeeperAlly Mage 0 Level(15,NPC,True) [16,26] 0 0 0 0 [Elfire, Thunder, Vulnerary] ChargeAIRecovery1
UNIT KeeperAlly Curate 0 Level(14,NPC,True) [18,28] 0 0 0 0 [Physic] [0xE,0xA,0x0,0x00] //Follow Lua and heal
UNIT KeeperAlly Vanguard 0 Level(12,NPC,True) [19,22] 0 0 0 0 [SteelAxe, SteelLance] ChargeAI 
UNIT KeeperAlly Warden 0 Level(13,NPC,True) [20,27] 0 0 0 0 [KillingEdge, Javelin] BossAI //blocking the arena, lol
UNIT

BeginningScene:
//ASMC ClearAllDebuffs // now clears tonics, too. If you add a prep screen to this chapter, then restarting the chapter after saving will remove any tonic boosts because this ASMC is being called each time. 
WEATHER 7
FadeToBlack
EraseBackground
//REMOVEALL_BLUE

LoadEnemies(CalistaTrapped)
LoadEnemies(SmallRebelGarrison)
LoadEnemies(MainAssault)
LoadEnemies(MiddleUnits)
LoadEnemies(RearUnits)
LoadEnemies(EnemiesAttackingCalista)

// TBD: Add Eventing to this

MUSC SwabbieTalkID
Text(HubC_2_BeginningScene1)
MUSC ASoldiersDutyID
Text(HubC_2_BeginningScene2)
Text(HubC_2_BeginningScene3)
Text(HubC_2_BeginningScene4)
Text(HubC_2_BeginningScene4_2)
Text(HubC_2_BeginningScene5)
 
Text(HubC_2_BeginningScene6)
MUSC StillZephyrsID

Text(HubC_2_BeginningScene7)
MUSC FinInTheCloudsID
Text(HubC_2_BeginningScene8)
Text(HubC_2_BeginningScene8_2)
Text(HubC_2_BeginningScene8_3)

PanToTileThenFlashCursorForXSeconds(9, 26, 60)

TILECHANGE 0x1 //callista is now exposed
AddTrap(MineTrapID,1,5)
AddTrap(MineTrapID,1,6)
AddTrap(MineTrapID,0,6)
SetUnitItemDurability(7, 16, 2, StrTonic) //Give valerie a str tonic
SetUnitItemDurability(14, 5, 1, RainbowTonic) //Give a fighter a droppable rainbow tonic
SetUnitItemDurability(0, 5, 3, 7) //Callista used 3 mines as preperation! 

GotoPrepScreen
NoFade
ENDA

Turn1_Explanation:
MUSC 0x7FFF
PanToTileThenFlashCursorForXSeconds(0, 5, 60)
CenterTextbox(C2ExplanationText_1)
PanToTileThenFlashCursorForXSeconds(19, 1, 60)
CenterTextbox(C2ExplanationText_2)
NoFade
ENDA

Turn3_Reinforcements:
LoadReinforcements(Reinforcements_T3)
NoFade
ENDA

Turn5_BridgeLowered:
//Turn 5, the right side bridge gets pushed down one space, allowing for more units to swoop in.
//This is used to set the enemy AIs here to 0. 
CameraCursor(19,1)
Text(C2BridgeLowered_T4)
CameraCursor(11,28)
STAL 30
SOUN 0x2D7 //snag falling
TILECHANGE 0x0
SVAL s1 0
CHAI [14, 5]
CHAI [17, 6]
CHAI [16, 5]
LoadReinforcements(Reinforcements_T5)
NoFade
ENDA

Turn8_SpawnGriffons:
LoadReinforcements(GriffonReinforcements)
NoFade
ENDA

Turn11_AggroBoss:
CameraCursor(19,1)
Text(C2_T11_CalistaAlive)
CameraCursor(11,28)
STAL 30
SVAL s1 0
CHAI BlackheartElite
CHAI BlackheartPlus
NoFade
ENDA

CalistaRecruitment:
MUSS WelcomeAboardMateyID
SetAI(Calista,0,0,0,0)
Text(CalistaRecruit_Generic)
CUSA Calista
MURE 2
NoFade
ENDA

EndingScene:
Text(C2_EndingScene1)
Text(C2_EndingScene1_2)
Text(C2_EndingScene1_3)
CHECK_ALIVE Calista // 4 variations of next scene, depending
BEQ 775 s0 sC
	CHECK_ALIVE Valerie
	BEQ 776 s0 sC
        MUSC CastawayHeartID
		Text(C2_EndingScene2_CalistaValerieAlive) //If both Calista and Valerie are alive
		GOTO 780
    LABEL 776
    MUSC CherishTheDeadID
    Text(C2_EndingScene2_CalistaAlive) // just calista
    GOTO 780
LABEL 775
CHECK_ALIVE Valerie
BEQ 778 s0 sC
    MUSC CherishTheDeadID
    Text(C2_EndingScene2_ValerieAlive) // just valerie
LABEL 778
MUSC CherishTheDeadID
Text(C2_EndingScene2_NoneAlive) //if you somehow got both killed
LABEL 780

MUSC ABlackheartsPrideID
Text(C2_EndingScene_Elodia)
FADI 15
STAL 60
FADU 15
Text(C2_EndingScene_Alonso)

GoToHubChapter(HubC)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN