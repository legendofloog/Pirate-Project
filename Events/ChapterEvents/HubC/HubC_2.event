EventPointerTable(HubC_2EventsID, PointerList)

//Some AI helpers for stuff going on in this chapter...
#define WaitOneTurnAI [0x0, 0x12, 0x4, 0x0]
#define ChargeAIRecovery1 [0x0, 0x0, 0x1, 0x0]

#define TC_C2CutsceneForest 	0x0
#define TC_C2CutsceneVillage	0x1
#define TC_C2CutsceneRetreat	0x2
#define TC_C2Snag		0x3
#define TC_C2Bridge 	0x4
#define TC_C2Chest 		0x5
#define TC_C2Village 	0x6

#define FlagC2_Village 0x20
#define FlagC2_Outpost 0x21


PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, Turn1_Explanation, 1)
TurnEventPlayer(0, Turn3_Reinforcements, 3)
TurnEventPlayer(0, Turn5_BridgeLowered, 5)
TurnEventPlayer(0, Turn8_SpawnWyverns, 8)
TurnEventPlayer(0, Turn11_AggroBoss, 11)
//TurnEventPlayer(0, EndingScene,1,255)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(9, CalistaRecruitment, 0x0, Calista) //anyone can talk with calista
END_MAIN

LocationBasedEvents:
Chest(DeadeyeBow, 21, 9)
Village(FlagC2_Village,HuntersMarkVillage,13,24)
END_MAIN

HuntersMarkVillage:
//SetBackground(HouseBG)
//Text(A2Village)
//EraseBackground
GiveItem(HuntersMark,0xFFFF)
NoFade
ENDA

MiscBasedEvents:
// You get the Charge scroll here, but something more unique could be added maybe
AREA FlagC2_Outpost HiddenOutpost [4,0][4,0] 
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
//Middle Units
UNIT Lua Captain 0 Level(10,Ally,False) [9,26] 0 0 0 0 [IronSword] NoAI
//Valerie
UNIT Valerie Archer_F 0 Level(5,Ally,True) [7,26] 0 0 0 0 [SteelBow, Snapshot, Tonic] NoAI
LoadUnit(11,26)
LoadUnit(8,27)
LoadUnit(10,27)
LoadUnit(12,27)
LoadUnit(9,28)
LoadUnit(11,28)
//Left side units
LoadUnit(1,26)
LoadUnit(3,26)
LoadUnit(1,27)
LoadUnit(2,27)
LoadUnit(2,28)
//adding a couple more units so we have one map with more units to play around with - Loog
LoadUnit(13,26)
LoadUnit(3,27)
UNIT

EnemiesAttackingCalista:
//First wave attacking the other captain

//This dude gets popped lmao, but his stats need to be nerfed because he's too good! 
UNIT BerserkerThatDiesC2 Berserker 0 Level(18,Enemy,True) [5,5] 0 0 0 0 [BraveAxe, BattleAxe, Swordreaver, Eyepatch] ChargeAI
UNIT BlackheartPlus Archer 0 Level(16,Enemy,True) [10,6] 0 0 0 0 [SteelBow] ChargeAI
UNIT BlackheartPlus Archer 0 Level(15,Enemy,True) [7,4] 0 0 0 0 [SteelBow] ChargeAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [7,2] 0 0 0 0 [SteelAxe] WaitOneTurnAI
UNIT

MainAssault:
//Fort units, left to right
UNIT BlackheartPlus Shaman 0 Level(16,Enemy,True) [8,21] 0 0 0 0 [Sagittae] GroupOneAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [11,21] 0 0 0 0 [SteelKnife] AttackInRangeAI
//Left Units
UNIT BlackheartPlus Warrior 0 Level(18,Enemy,True) [0,15] 0 0 0 0 [SteelAxe] GroupOneAI
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [1,19] 0 0 0 0 [SteelAxe, HandAxe] GroupOneAI
UNIT BlackheartPlus Hunter 0 Level(16,Enemy,True) [3,21] 0 0 0 0 [Longbow] GroupOneAI
UNIT BlackheartPlus Champion 0 Level(17,Enemy,True) [2,24] 0 0 0 0 [Javelin, SilverLance, Vulnerary] GroupOneAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [7,15] 0 0 0 0 [SteelAxe, Vulnerary] ChargeAI
//Middle Units
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [5,17] 0 0 0 0 [ArcaneKnife] ChargeAI 
UNIT BlackheartPlus Hero 0 Level(17,Enemy,True) [9,23] 0 0 0 0 [SilverSword, Elixir] ChargeAI
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [10,22] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus Vanguard 0 Level(16,Enemy,True) [9,18] 0 0 0 0 [HandAxe, Vulnerary] ChargeAI
//Right Units
UNIT BlackheartElite Sniper 0 Level(18,Enemy,True) [16,17] 0 0 0 0 [SilverBow] ChargeAI
UNIT BlackheartPlus Deserter 0 Level(16,Enemy,True) [15,18] 0 0 0 0 [SteelLance] ChargeAI
UNIT BlackheartPlus Mercenary 0 Level(16,Enemy,True) [17,19] 0 0 0 0 [SteelSword] ChargeAI
UNIT BlackheartPlus Berserker 0 Level(17,Enemy,True) [15,15] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus Shaman 0 Level(16,Enemy,True) [15,12] 0 0 0 0 [Luna, SecretBook] WaitOneTurnAI
UNIT BlackheartPlus Gunner 0 Level(16,Enemy,True) [16,11] 0 0 0 0 [Blunderbuss] WaitOneTurnAI
UNIT BlackheartPlus WyvernRider 0 Level(16,Enemy,True) [19,12] 0 0 0 0 [Tomahawk, TealGem] WaitOneTurnAI
UNIT 

MiddleUnits:
UNIT BlackheartPlus WarMonk 0 Level(17,Enemy,True) [9,14] 0 0 0 0 [IronAxe, Silence] BossAI
UNIT BlackheartPlus WarMonk 0 Level(17,Enemy,True) [10,14] 0 0 0 0 [HandAxe, Physic] [0xE, 0x3, 0x4, 0x20]
UNIT BlackheartElite Sniper 0 Level(18,Enemy,True) [11,13] 0 0 0 0 [Greatbow, ShiningBow] BossAI
UNIT BlackheartPlus Deserter 0 Level(16,Enemy,True) [8,13] 0 0 0 0 [Impaler, Javelin] WaitOneTurnAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [12,10] 0 0 0 0 [SilverDagger] ChargeAI 

//Right middle, protecting the chests
UNIT BlackheartPlus Gunner 1 Level(17,Enemy,True) [21,9] 0 0 0 0 [Culverin, InfiltratorGun] GroupTwoAI
UNIT BlackheartPlus Hero_F 1 Level(18,Enemy,True) [21,12] 0 0 0 0 [SilverSword] WaitOneTurnAI

//First left stair formation
UNIT BlackheartPlus Marksman 0 Level(18,Enemy,True) [4,14] 0 0 0 0 [Pistol, Pistol, HideShield] GroupFourAI
UNIT BlackheartPlus Archer 0 Level(16,Enemy,True) [5,13] 0 0 0 0 [SteelBow] GroupFourAI //3-13 archer is back!!
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [3,13] 0 0 0 0 [Hammer, Elixir] GroupFourAI

//Second left stair formation, holding the stairs close to captain, but not attacking Calista

UNIT BlackheartPlus Trickster 1 Level(17,Enemy,True) [7,9] 0 0 0 0 [Elfire, IronKnife] GroupThreeAI
UNIT BlackheartPlus Mercenary 1 Level(16,Enemy,True) [6,8] 1 0 0 0 [SilverSword] GroupThreeAI
UNIT BlackheartPlus Deserter 1 Level(16,Enemy,True) [8,8] 0 0 0 0 [Spear] GroupThreeAI
UNIT

RearUnits:

UNIT BlackheartElite Sniper 0 Level(18, Enemy, True) [12,3] 0 0 0 0 [Greatbow] AttackInRangeAI
UNIT BlackheartPlus Warrior 0 Level(18, Enemy, True) [8,1] 0 0 0 0 [SilverAxe, IronBow] AttackInRangeAI

//Boss group

UNIT Salvador 0 0 Level(20,Enemy,True) [18,1] 0 0 0 0 [SilverLance, KillingEdge, BravesCrest] AttackInRangeAI
UNIT BlackheartPlus Assassin 1 Level(18,Enemy,True) [17,0] 0 0 0 0 [SteelKnife, Stiletto] AttackInRangeAI
UNIT BlackheartPlus Marksman 0 Level(18,Enemy,True) [19,2] 0 0 0 0 [InfiltratorGun] AttackInRangeAI

//These guys are going to go charge on Turn 4, when the bridge gets dropped
UNIT BlackheartPlus Fighter 0 Level(16, Enemy, True) [14,5] 0 0 0 0 [HandAxe, Tonic] AttackInRangeAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) [17,6] 0 0 0 0 [SteelKnife, SteelDagger] AttackInRangeAI
UNIT BlackheartPlus Hero 0 Level(18,Enemy,True) [16,5] 0 0 0 0 [SteelSword, HandAxe, Elixir] AttackInRangeAI
UNIT

Reinforcements_T3:
//Small trickle of weak units to attack Calista
UNIT BlackheartPlus Fighter 0 Level(16,Enemy,True) [14,0] 0 0 1 REDA12R0 [IronAxe, HandAxe] ChargeAI
UNIT BlackheartPlus Mercenary_F 0 Level(16,Enemy,True) [15,0] 0 0 1 REDA13R0 [SteelSword] ChargeAI
UNIT

Reinforcements_T5:
//Reinforcements to help aid bridge attack
UNIT BlackheartElite Sniper 0 Level(18,Enemy,True) [21,6] 0 0 1 REDA19R6 [SilverBow] ChargeAI
UNIT BlackheartPlus Mercenary 0 Level(16,Enemy,True) [21,5] 0 0 1 REDA19R5 [Onigiri] ChargeAI
UNIT

//Meant to kill the imperial captain if you aren't quick enough! 
GriffonReinforcements:
UNIT BlackheartPlus DragonRider 0 Level(16,Enemy,True) [3,0] 0 0 0 0 [Halberd] ChargeAI
UNIT BlackheartPlus Dracoknight 0 Level(18,Enemy,True) [4,0] 0 0 0 0 [Swordreaver] ChargeAI
UNIT

CalistaTrapped:
UNIT Calista Dragoon 0 Level(18,NPC,True) [0,5] 0 0 0 0 [Toradar, SilverSword, HandCannon, Elixir] BossHealAI
UNIT

// The keeper units have slightly improved stats that'll let them handle their own early in Hub C,
// but will likely get overwhelmed if you do this chapter later. 
SmallRebelGarrison:
UNIT KeeperC2 Halberdier 0 Level(17,NPC,True) [17,24] 0 0 0 0 [SilverLance, Axereaver, Elixir] ChargeAIRecovery1 //ChargeAI, but with recovery mode 30% HP. 
UNIT KeeperC2 Mercenary 0 Level(16,NPC,True) [15,24] 0 0 0 0 [SteelSword, Wingclipper, IronShield, Vulnerary] ChargeAIRecovery1
UNIT KeeperC2 Mage 0 Level(15,NPC,True) [16,26] 0 0 0 0 [Elfire, Thunder, Vulnerary] ChargeAIRecovery1
UNIT KeeperC2 Curate 0 Level(14,NPC,True) [18,28] 0 0 0 0 [Physic] [0xE,0xA,0x0,0x00] //Follow Lua and heal
UNIT KeeperC2 Vanguard 0 Level(12,NPC,True) [19,22] 0 0 0 0 [SteelAxe, SteelLance] ChargeAI 
UNIT KeeperC2 Warden 0 Level(13,NPC,True) [20,27] 0 0 0 0 [KillingEdge, Javelin] BossAI //blocking the arena, lol
UNIT

IntroCutsceneBlackhearts_Forest:
UNIT Salvador 0 0 Level(20,Enemy,True) 				[7,9] 	0 0 0 0 [KillingEdge, KillerLance, FliersPermit] AttackInRangeAI
UNIT BlackheartPlus Sniper 0 Level(16,Enemy,True) 	[2,10] 0 0 0 0 [Greatbow, MiniBow] BossAI
UNIT BlackheartPlus Deserter 0 Level(16,Enemy,True) [1,7] 	0 0 0 0 [Impaler, Javelin] WaitOneTurnAI
UNIT BlackheartPlus Ninja 0 Level(16,Enemy,True) 	[0,9] 0 0 0 0 [SilverDagger] ChargeAI 
UNIT BlackheartPlus Berserker 0 Level(17,Enemy,True)[8,8] 0 0 0 0 [SteelAxe] ChargeAI
UNIT BlackheartPlus Gunner 0 Level(16,Enemy,True) 	[10,2] 0 0 0 0 [Blunderbuss] WaitOneTurnAI
//Everyone below will move around during the cutscenes...
UNIT 0x50 			Warrior 0 Level(18,Enemy,True) 	[8,0] 	0 0 0 0 [SteelAxe] GroupOneAI
UNIT 0x51	 		Fighter 0 Level(16,Enemy,True) 	[4,9] 0 0 0 0 [SteelLance] ChargeAI
UNIT 0x52 			Assassin 1 Level(19,Enemy,True)	[12,0] 	0 0 0 0 [SteelKnife, Stiletto] AttackInRangeAI
//These guys move in a bit to show how many blackhearts there are
UNIT 0x53 			Mercenary 0 Level(16,Enemy,True)[11,13] 0 0 0 0 [SteelSword] ChargeAI
UNIT 0x54 			WarMonk 0 Level(17,Enemy,True)	[18, 4] 0 0 0 0 [HandAxe, Physic] [0xE, 0x3, 0x4, 0x20]
UNIT 0x55 			Fighter 0 Level(16,Enemy,True) 	[18,1] 	0 0 0 0 [SteelAxe, HandAxe] GroupOneAI
UNIT 0x56 			Hunter 0 Level(16,Enemy,True) 	[17,5] 	0 0 0 0 [Longbow] GroupOneAI
UNIT 0x57			Shaman 0 Level(16,Enemy,True) 	[12,7] 0 0 0 0 [Luna, SecretBook] WaitOneTurnAI
//even more blackheart forces to fill in the gaps once they turn hostile
UNIT 0x58 			Marksman 1 Level(17,Enemy,True) [16,3] 0 0 0 0 [Culverin, InfiltratorGun] GroupTwoAI
UNIT 0x59 			Hunter 1 Level(15,Enemy,True) 	[16,4] 0 0 0 0 [Longbow, Vulnerary] GroupTwoAI
UNIT 0x5A 			Hero_F 1 Level(18,Enemy,True) 	[16,5] 0 0 0 0 [SilverSword] WaitOneTurnAI
UNIT

IntroCutsceneImperials_Forest:
UNIT Valerie Archer_F 0 Level(5,Enemy,True) 	[5,2] 0 0 0 0 [SteelBow, Snapshot, Tonic,] NoAI
UNIT Calista Dragoon 0 Level(18,Enemy,True) [5,5] 0 0 0 0 [Toradar, SteelSword, Elixir, Mine] BossAI
//use 0x32 - 0x35 for allegience changes
UNIT 0x32 Gunner 0 Level(3, Enemy, True) 	[3,5] 0 0 0 0 [SteelLance] ChargeAI //imperial recruit
UNIT 0x33 SwordCav 0 Level(3, Enemy, True) 	[4,3] 0 0 0 0 [] ChargeAI
UNIT 0x34 Soldier 0 Level(3, Enemy, True) 	[3,2] 0 0 0 0 [] ChargeAI
UNIT 0x35 Knight 0 Level(3, Enemy, True) 	[2,4] 0 0 0 0 [] ChargeAI
UNIT

IntroCutsceneBlackheart_Scout:
UNIT 0x36 WyvernRider 0 Level(16,Enemy,True) 	[0,8] 	0 0 0 0 [Longbow] GroupOneAI
UNIT

IntroCutsceneVillage_FirstArrival:
UNIT Khazhak Fighter 0 Level(4,NPC,True) [14,21] 0x0 0x0 0 0 [0x0] NoAI
UNIT Orlando 0 0 Level(4,Ally,True) [15,22] 0x0 0x0 0 0 [0x0] NoAI
UNIT Lua Captain 0 Level(10,Ally,False) [14,22] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [13,22] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [14,23] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel VillagerMAdult 0 Level(4,NPC,True) [6,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel VillagerMChild 0 Level(4,NPC,True) [20,16] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel VillagerFAdult 0 Level(4,NPC,True) [16,13] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel VillagerFAdult 0 Level(4,NPC,True) [0,19] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel VillagerMAdult 0 Level(4,NPC,True) [6,10] 0x0 0x0 0 0 [0x0] NoAI
//actual armed rebels, not many of them since most are on a mission 
UNIT Rebel Warden 0 Level(4,NPC,True) [20,11] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel Mage 0 Level(4,NPC,True) [12,17] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel Mercenary 0 Level(4,NPC,True) [3,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel Halberdier 0 Level(4,NPC,True) [10,12] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel Curate 0 Level(4,NPC,True) [7,15] 0x0 0x0 0 0 [0x0] NoAI
UNIT Rebel Fighter 0 Level(4,NPC,True) [7,10] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneImperialDuoFleeing:
UNIT Calista Dragoon 0 Level(18,NPC,True) [0,5] 0 0 0 0 [Toradar, SilverSword, Elixir, Mine] BossAI
UNIT


IntroCutsceneVillage_AttackIncoming:
UNIT Lua Captain 0 Level(10,Ally,False) [10,15] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar 0 0 Level(1,Ally,True) [13,15] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [8,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Orlando 0 0 Level(4,Ally,True) [12,15] 0x0 0x0 0 0 [0x0] NoAI
UNIT Khazhak Fighter 0 Level(4,NPC,True) [11,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x91 VillagerMAdult 0 Level(4,NPC,True) [9,16] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x51 VillagerMChild 0 Level(4,NPC,True) [20,16] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x52 VillagerFAdult 0 Level(4,NPC,True) [16,13] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x53 VillagerFAdult 0 Level(4,NPC,True) [0,19] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x54 VillagerMAdult 0 Level(4,NPC,True) [6,10] 0x0 0x0 0 0 [0x0] NoAI
//actual armed rebels, not many of them since most are on a mission 
UNIT Rebel Warden 0 Level(4,NPC,True) [20,11] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0xAA Mage 0 Level(4,NPC,True) [12,17] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0xAB Mercenary 0 Level(4,NPC,True) [3,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0xAC Halberdier 0 Level(4,NPC,True) [15,17] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0xAD Curate 0 Level(4,NPC,True) [17,14] 0x0 0x0 0 0 [0x0] NoAI
UNIT Valerie Archer_F 0 Level(5,NPC,True) [9,4] 0 0 0 0 [SteelBow, Snapshot, Tonic] NoAI
//these guys actively move once pirates spotted
UNIT 0x60 Knight 0 Level(4,NPC,True) [11,5] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x61 Soldier 0 Level(4,NPC,True) [8,5] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x62 Vanguard 0 Level(4,NPC,True) [3,5] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x63 Fighter 0 Level(4,NPC,True) [13,6] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x64 Deserter 0 Level(4,NPC,True) [5,7] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x90 SwordCav 0 Level(4,NPC,True) [12,9] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x66 Gunner 0 Level(4,NPC,True) [4,11] 0x0 0x0 0 0 [0x0] NoAI
UNIT 0x67 LanceCav 0 Level(4,NPC,True) [2,9] 0x0 0x0 0 0 [0x0] NoAI
UNIT 

//these guys are going to appear in-frame, starting to take position
IntroCutsceneVillage_PiratesSwarming:
//Left Units
UNIT 0x71 Hunter 0 Level(16,Enemy,True) [0,0] 0 0 0 0 [Longbow] GroupOneAI
UNIT 0x72 Champion 0 Level(17,Enemy,True) [1,1] 0 0 0 0 [Javelin, SilverLance, Vulnerary] GroupOneAI
//Middle Units
UNIT 0x73 Shaman 0 Level(16,Enemy,True) [7,2] 0 0 0 0 [Sagittae] GroupOneAI
UNIT 0x74 Ninja 0 Level(16,Enemy,True) [14,2] 0 0 0 0 [SteelKnife] AttackInRangeAI
UNIT 0x75 Hero 0 Level(17,Enemy,True) [9,2] 0 0 0 0 [SilverSword, Elixir] ChargeAI
UNIT 0x76 Fighter 0 Level(16,Enemy,True) [10,1] 0 0 0 0 [SteelAxe] ChargeAI
UNIT

BeginningScene:
/*
//ASMC ClearAllDebuffs // now clears tonics, too. If you add a prep screen to this chapter, then restarting the chapter after saving will remove any tonic boosts because this ASMC is being called each time. 
WEATHER 7

TILECHANGE TC_C2CutsceneForest
CAMERA_CENTERED [0,0]
MUSC WindGustSoundID
//Inner monologue from Calista, the captain 
//that was first encountered in A-4.
SetBackground(0x35)
Text(Hub_C2_Intro_1)

//scene fades from black, showing mixture of
//imperial and blackheart forces in the woods

MUSC 0x7FFF
LoadCutsceneUnits(IntroCutsceneImperials_Forest)
LoadCutsceneUnits(IntroCutsceneBlackhearts_Forest)
STAL 15
//its just a bird
MUSC 0x52
EraseBackground
FADU 5
STAL 30
FlashCursor(Valerie, 60)
STAL 60
MOVE 2 Valerie [6,4]
ENUN
FlashCursor(Calista, 30)
Text(Hub_C2_Intro_2A)

//Salvador and a pirate move up to harass
MOVE 4 Salvador [6,6]
MOVE 4 0x51 [4,6]
ENUN
FlashCursor(Salvador, 60)
Text(Hub_C2_Intro_2B)

//Salvador moves off screen, leaving the imperial troops to talk among themselves
MOVE 4 Salvador [9,9]
MOVE 4 0x51 [4,11]
ENUN
STAL 20
MOVE 3 0x34 [4,4]
ENUN
FlashCursor(Calista, 60)
Text(Hub_C2_Intro_3A)

//now a pirate scout comes in
LoadCutsceneUnits(IntroCutsceneBlackheart_Scout)
MOVE 5 0x36 [6,9]
STAL 30
MOVE 3 Salvador [7,9]
ENUN
FlashCursor(0x36, 60)
Text(Hub_C2_Intro_3B)

//Calista comes over to investigate
MOVE 3 Calista [5,7]
STAL 30
MOVE 2 Valerie [5,4]
ENUN
FlashCursor(Calista, 60)
Text(Hub_C2_Intro_4A)

//Wyvern flies off to alert men,
//some go ahead and move in. 
//Calista goes in to confront him

MOVE 7 0x36 [16,9]
MOVE 3 0x53	[11,10]
MOVE 3 0x54	[13,4]
MOVE 3 0x55 [14,1] 
MOVE 3 0x56 [14,8] 
STAL 30
MOVE 3 Calista [6,8]
ENUN
Text(Hub_C2_Intro_4B)

//Valerie overhears her captain's hesitant
//approval, and rushes in to convince her otherwise

MOVE 5 Valerie [5,7]
ENUN
STAL 20
FlashCursor(Valerie, 60) 
STAL 60
TEXTSTART
TEXTSHOW Hub_C2_Intro_5
TEXTEND
SOUN 0x7FFF //birds stop chirping in the middle
STAL 15
TEXTCONT
TEXTEND
REMA

STAL 15
//Pirates turn hostile, start moving in...
SOUN AvastMeHeartiesID
MUSI 

MOVE 5 0x50 [5,1] //warrior
MOVE 5 0x51 [3,8] //fighter 
MOVE 5 0x52 [8,4] //assassin
MOVE 5 0x53 [8,6] //merc
MOVE 5 0x54 [11,4]//warmonk
MOVE 5 0x55 [8,2] //fighter2
MOVE 5 0x56 [9,5] //hunter
MOVE 5 0x57 [10,6] //shaman
MOVE 5 0x58 [13,2] //marksman
MOVE 5 0x59 [12,9] //hunter2
MOVE 5 0x5A [12,7] //hero
ENUN
//Calista and Valerie fall back
MOVE 5 Calista [5,6]
MOVE 5 Valerie [6,5]
STAL 70
MOVE 3 Salvador [5,8]
ENUN
STAL 30
FlashCursor(Calista,60)
Text(Hub_C2_Intro_6A)

//Now all imperials turn green, move into their own positions 


CUSN 0x32 //gunner
CUSN 0x33 //cav
CUSN 0x34 //soldier
CUSN 0x35 //knight
CUSN Valerie 
CUSN Calista
MOVE 7 0x35 [3,2]
MOVE 7 0x33 [1,5]
MOVE 7 0x34 [3,6]
MOVE 7 0x32 [0,4]
MOVE 7 Valerie [2,3]
MOVE 7 Calista [4,5]
STAL 60 
FlashCursor(Salvador,60)
Text(Hub_C2_Intro_6B)


//fade to black, moving to village
EVBIT_F 2 //stop scene skip here for tile changes
MUSC 0x7FFF
FADI 10
STAL 30
TILEREVERT TC_C2CutsceneForest
CLEN 0
CLEE 0
TILECHANGE TC_C2CutsceneVillage
MUSC StillZephyrsID
LoadCutsceneUnits(IntroCutsceneVillage_FirstArrival)
CAMERA_CENTERED [14,16]
FADU 5

//Village time. 
STAL 60
MOVE 5 Khazhak 	[14,14]
STAL 20
MOVE 5 Lua 		[13,15]
STAL 15
MOVE 5 Pilar 	[15,15]
STAL 15
MOVE 5 Razvan 	[14,16]
ENUN
FlashCursor(Khazhak, 60)
Text(Hub_C2_Intro_7)

//negotiations begin, fade to black

FADI 5
STAL 60
REMOVEALL_BLUE
REMOVEALL_GREEN
LoadCutsceneUnits(IntroCutsceneVillage_AttackIncoming)
CAMERA_CENTERED [11,15]
FADU 5
STAL 30
FlashCursor(Lua, 60)
TEXTSTART
TEXTSHOW Hub_C2_Intro_9
TEXTEND
SOUN 0x7FFF //pause music
STAL 15
TEXTCONT
TEXTEND

//commotion at the gates

MUSC 0x7FFF
CAMERA_CENTERED [8,8]
//lua moves towards the commotion
MOVE 4 Lua [9,10]
MUSC SwabbieTalkID
ENUN
STAL 60
Text(Hub_C2_Intro_10)

//valerie comes forward, while rebels
//move in to rapidly intercept
MOVE 5 Valerie [9,9]
MOVE 8 0x67 [0,1]
MOVE 7 0x66 [1,2] 
MOVE 4 0x54 [5,9] //villager 
STAL 10
REMOVEUNIT 0x54
ENUN
MOVE 8 0x90 [9,1]
MOVE 7 0x64 [8,2]
MOVE 7 0x63 [11,2]
MOVE 5 Razvan [10,10]
ENUN
STAL 5
FlashCursor(Valerie,40)
Text(Hub_C2_Intro_11)

//The supplier and other units
//come up out of curiosity
MOVE 30 0xAC [13,15] //halb
MOVE 30 0xAA [14,16] //mage
MOVE 30 0xAB [14,14] //merc
MOVE 30 0xAD [15,15] //cura
CAMERA_CENTERED [8,12]
MOVE 5 Khazhak [9,12]
MOVE 5 Pilar [11,11]
MOVE 5 Orlando [8,13]
ENUN 
STAL 15
FlashCursor(Khazhak,60)
Text(Hub_C2_Intro_12)
ENUF 0x84 //need to disable this in order to see HP bars



//injured cavalier rushes
//down, warns about enemies
SVAL s1 0x07 //set hp to 7
SET_HP 0x90
MOVE 9 0x90 [11,12]
ENUN
FlashCursor(0x90, 60)
Text(Hub_C2_Intro_13)

//cavalier finds more reinforcements,
//Khazhak moves out with remaining troops
MOVE 9 0x90 [13,21]
MOVE 6 Khazhak [18,14]
ENUN
MOVE 6 0xAC [20,15] //halb
STAL 30
MOVE 5 0xAA [19,16] //mage
MOVE 5 0xAB [19,14] //merc
MOVE 4 0xAD [19,15] //cura
ENUN
ENUT 0x84 //re-enabled, boss bars no longer wanted
REMOVEUNIT 0x60
REMOVEUNIT 0x61
REMOVEUNIT 0x62
LoadCutsceneUnits(IntroCutsceneVillage_PiratesSwarming)
STAL 10
CAMERA_CENTERED [8,8]
STAL 50
MOVE 5 0x73 [8,5]
MOVE 5 0x74 [11,5]
STAL 15
MOVE 6 0x75 [9,6]
MOVE 6 0x76 [10,4]
MOVE 6 0x72 [2,6]
MOVE 6 0x71 [1,5]
STAL 30
MOVE 6 Valerie [7,10]
MOVE 6 Razvan [9,12]
MOVE 6 Orlando [8,11]
MOVE 6 Pilar [10,11]

STAL 60
Text(Hub_C2_Intro_14)

STAL 60
EraseBackground
FadeToBlack
TILEREVERT TC_C2CutsceneVillage
REMOVEALL_BLUE
REMOVEALL_GREEN
REMOVEALL_RED

//CenterTextbox(C2_Synopsis_A)
//REMOVEALL_BLUE
*/
// TBD: Add Eventing to this
LoadEnemies(CalistaTrapped)
LoadEnemies(SmallRebelGarrison)
LoadEnemies(MainAssault)
LoadEnemies(MiddleUnits)
LoadEnemies(RearUnits)
LoadEnemies(EnemiesAttackingCalista)

SetBackground(Woodland) // the forest
MUSC SwabbieTalkID
Text(HubC_2_BeginningScene1)
MUSC ASoldiersDutyID
Text(HubC_2_BeginningScene2)
Text(HubC_2_BeginningScene3)
Text(HubC_2_BeginningScene4)
Text(HubC_2_BeginningScene4_2)
Text(HubC_2_BeginningScene5)
 
Text(HubC_2_BeginningScene6)
MUSC StillZephyrsID

SetBackground(Village)
Text(HubC_2_BeginningScene7)
MUSC FinInTheCloudsID
Text(HubC_2_BeginningScene8)
Text(HubC_2_BeginningScene8_2)
Text(HubC_2_BeginningScene8_3)

FADI 15
TILECHANGE TC_C2Snag //callista is now exposed...
AddTrap(MineTrapID,1,5) //...but she's got explosives, baby. 
AddTrap(MineTrapID,1,6)
AddTrap(MineTrapID,0,6)
LoadEnemies(ValerieInDaSquad)
SetUnitItemDurability(Valerie, 2, StrTonic) //Give valerie a str tonic
SetUnitItemDurability(14, 5, 1, RainbowTonic) //Give a fighter a droppable rainbow tonic
GotoPrepScreen
NoFade
ENDA

ValerieInDaSquad:
UNIT Valerie Archer_F 0 Level(5,Ally,True) [7,26] 0 0 0 0 [SteelBow, Snapshot, Tonic] NoAI
UNIT

HiddenOutpost:
//SetBackground(HouseBG)
//Text(A2Village)
//EraseBackground
GiveItemWithDurability(SkillScroll,ChargeID,0xFFFF)
NoFade
ENDA

Turn1_Explanation:
MUSC 0x7FFF
PanToTileThenFlashCursorForXSeconds(0, 5, 60)
CenterTextbox(C2ExplanationText_1)
PanToTileThenFlashCursorForXSeconds(19, 1, 60)
CenterTextbox(C2ExplanationText_2)
NoFade
ENDA

Turn3_Reinforcements:
LoadReinforcements(Reinforcements_T3)
NoFade
ENDA

Turn5_BridgeLowered:
//Turn 5, the right side bridge gets pushed down one space, allowing for more units to swoop in.
CameraCursor(19,1)
Text(C2BridgeLowered_T4)
CameraCursor(11,28)
STAL 30
SOUN 0x2D7 //snag falling
TILECHANGE TC_C2Bridge
//This is used to set the enemy AIs here to 0. 
SVAL s1 0
CHAI [14, 5]
CHAI [17, 6]
CHAI [16, 5]
LoadReinforcements(Reinforcements_T5)
NoFade
ENDA

Turn8_SpawnWyverns:
LoadReinforcements(GriffonReinforcements)
NoFade
ENDA

Turn11_AggroBoss:
CameraCursor(19,1)
Text(C2_T11_CalistaAlive)
CameraCursor(11,28)
STAL 30
SVAL s1 0
CHAI BlackheartElite
CHAI BlackheartPlus
NoFade
ENDA

CalistaRecruitment:
MUSS WelcomeAboardMateyID
SetAI(Calista,0,0,0,0)
Text(CalistaRecruit_Generic)
CUSA Calista
MURE 2
NoFade
ENDA

EndingScene:
FADI 15
FadeOutMusic
STAL 60
MUSC StillZephyrsID
SetBackground(Village)
Text(C2_EndingScene1)
Text(C2_EndingScene1_2)
Text(C2_EndingScene1_3)
CHECK_ALIVE Calista // 4 variations of next scene, depending
BEQ 775 s0 sC
	CHECK_ALIVE Valerie
	BEQ 776 s0 sC
        MUSC CastawayHeartID
		Text(C2_EndingScene2_CalistaValerieAlive) //If both Calista and Valerie are alive
		GOTO 780
    LABEL 776
    MUSC CherishTheDeadID
    Text(C2_EndingScene2_CalistaAlive) // just calista
    GOTO 780
LABEL 775
CHECK_ALIVE Valerie
BEQ 778 s0 sC
    MUSC CherishTheDeadID
    Text(C2_EndingScene2_ValerieAlive) // just valerie
LABEL 778
MUSC CherishTheDeadID
Text(C2_EndingScene2_NoneAlive) //if you somehow got both killed
LABEL 780
FADI 15
STAL 60
SetBackground(Woodland)
MUSC ASoldiersDutyID
Text(C2_EndingScene_Elodia)
FADI 15
STAL 60
FADU 15
MUSC ABlackheartsPrideID
Text(C2_EndingScene_Alonso)
GoToHubChapter(HubC)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN