EventPointerTable(HubDEventsID, PointerList)

#define SeenHubD1Pitch 7
#define SeenHubD2Pitch 8
#define SeenHubD3Pitch 9
#define Flag_HubD_CaptainsQuarters_ToDeck 10
#define Flag_HubD_DiningQuarters_ToDeck 11
#define Flag_HubD_LivingQuarters_ToDeck 12
#define Flag_HubD_DeckToOtherAreas 13
#define Flag_HubD_CaptainsQuarters_ToEnd 14


PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene


TurnBasedEvents:
TurnEventPlayer(0, PostPrepsEvent, 1)
END_MAIN

PostPrepsEvent:
FADI 15
CALL SpawnHubD_DeckUnits
CALL HubD_Traps_Deck
//CALL SetAllHubDTalkFlags
CAMERA_CENTERED Lua
ENUT Flag_HubD_CaptainsQuarters_ToDeck
ENUT Flag_HubD_DiningQuarters_ToDeck
ENUT Flag_HubD_LivingQuarters_ToDeck //turn off all flags except the ones that go to floor 1
ENUT Flag_HubD_CaptainsQuarters_ToEnd
ENUF Flag_HubD_DeckToOtherAreas
FADU 15
CheckHowManyHubChaptersCompleted
BNE 2 sC s0 //if chapters completed != 0, skip
    CenterTextbox(HubDArmadaExplanation) //initial explanation
LABEL 2
SVAL s1 3 //only 3, this time around
BNE 3 sC s1
    CenterTextbox(HubDSayWhereToGo) //note that you can move on
LABEL 3
NoFade
ENDA

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
AREA Flag_HubD_CaptainsQuarters_ToEnd EndingScene [7,8] [8,8]
AREA Flag_HubD_DeckToOtherAreas GotoCaptainQuarters [7,14] [8,14]
AREA Flag_HubD_DeckToOtherAreas GotoDiningQuarters [5,20] [5,20]
AREA Flag_HubD_DeckToOtherAreas GotoDiningQuarters [10,20] [10,20]
AREA Flag_HubD_DeckToOtherAreas GotoLivingQuarters [11,23] [11,24]
AREA Flag_HubD_CaptainsQuarters_ToDeck GotoDeck [7,14] [8,14]
AREA Flag_HubD_DiningQuarters_ToDeck GotoDeck [5,20] [5,20]
AREA Flag_HubD_DiningQuarters_ToDeck GotoDeck [10,20] [10,20]
AREA Flag_HubD_LivingQuarters_ToDeck GotoDeck [11,23] [11,24]
AREA 0 SwitchToFlyingMode [7,8] [8,8]
AFEV 0 RefreshLua 0
END_MAIN

GotoCaptainQuarters:
CALL RemoveAllTraps
ENUF Flag_HubD_CaptainsQuarters_ToDeck
InitiateHubDTileChange(HubD_Traps_CaptainsQuarters, 0, SpawnHubD_DeckUnits)
NoFade
ENDA

GotoDiningQuarters:
CALL RemoveAllTraps
ENUF Flag_HubD_DiningQuarters_ToDeck
InitiateHubDTileChange(HubD_Traps_DiningQuarters, 1, SpawnHubD_DeckUnits)
NoFade
ENDA

GotoLivingQuarters:
CALL RemoveAllTraps
ENUF Flag_HubD_LivingQuarters_ToDeck
InitiateHubDTileChange(HubD_Traps_LivingQuarters, 2, SpawnHubD_DeckUnits)
NoFade
ENDA

GotoDeck:
CALL RemoveAllTraps
ENUF Flag_HubD_DeckToOtherAreas
InitiateHubDTileRevertAll(HubD_Traps_Deck, SpawnHubD_DeckUnits)
NoFade
ENDA

HubD_Traps_CaptainsQuarters:
AddTrap(BlueSigilTrapID, 7, 14)
AddTrap(BlueSigilTrapID, 8, 14)
CHECK_EVENTID GoToNextHubFlagID //has to be unset to be "on"
BNE 776 s0 sC
    AddTrap(GreenSigilTrapID, 7, 8)
    AddTrap(GreenSigilTrapID, 8, 8)
    ENUF Flag_HubD_CaptainsQuarters_ToEnd //unset this flag if we're in this space only
LABEL 776
NoFade
ENDA

HubD_Traps_DiningQuarters:
AddTrap(BlueSigilTrapID, 5, 20)
AddTrap(BlueSigilTrapID, 10, 20)
NoFade
ENDA

HubD_Traps_LivingQuarters:
AddTrap(BlueSigilTrapID, 11, 23)
AddTrap(BlueSigilTrapID, 11, 24)
NoFade
ENDA

RemoveAllTraps: //clears all traps, disables all hub movement flags
RemoveTrapAtCoords(7,8)
RemoveTrapAtCoords(8,8)
RemoveTrapAtCoords(7,14)
RemoveTrapAtCoords(8,14)
RemoveTrapAtCoords(5,20)
RemoveTrapAtCoords(10,20)
RemoveTrapAtCoords(11,23)
RemoveTrapAtCoords(11,24)
ENUT Flag_HubD_DeckToOtherAreas
ENUT Flag_HubD_CaptainsQuarters_ToDeck
ENUT Flag_HubD_DiningQuarters_ToDeck 
ENUT Flag_HubD_LivingQuarters_ToDeck
ENUT Flag_HubD_CaptainsQuarters_ToEnd
NoFade
ENDA

SwitchToFlyingMode:
FADI 15
SET_ACTIVE 0
STAL 30
RemoveAll
LOMA HubDTheOutskirts
LOAD1 1 LiberatorShipGroup
ENUN
//LoadNPCUnitGroupIfEIDNotSet(PassarolaRecruitedFlag, 1337, PassarolaUnitGroup)
SET_ACTIVE Liberator
CAMERA_CENTERED Liberator
HIDEUNIT Lua
CALL SpawnInHubDQuests
ENUN
FADU 15
NoFade
ENDA

LiberatorShipGroup:
UNIT Liberator Airship 0 Level(20,Ally,False) [25,15] 0 0 0 0 [0x0] NoAI
UNIT

RefreshLua:
SVAL s1 0x42
UNSET_CONDITION Lua
NoFade
ENDA

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [7,12] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

TestEnemyGroup:
UNIT Rebel MasterNinja 0 Level(25,Enemy,True) [8,11] 0 0 0 0 [IronKnife] NoAI
UNIT Rebel Seraph 0 Level(25,Enemy,True) [6,11] 0 0 0 0 [IronSword] NoAI
UNIT


BeginningScene:
WEATHER 7
ENUT LuaInAHubFlag
ENUT GoToNextHubFlagID
CheckHowManyHubChaptersCompleted
BNE 2 sC s0 //first time seeing it in this case, play first time event (both meant to take place in the captain's quarters)
    SetBackground(Inn)
    MUSC OrlandosThemeID
    Text(HubDBeginningScene1)
    Text(HubDBeginningScene2)
    GOTO 3
LABEL 2
SVAL s1 3
BNE 3 sC s1 //if chapters completed != 3, skip
    SetBackground(Inn)
    MUSC OrlandosThemeID
    Text(HubDEndingScene1)
    Text(HubDEndingScene2)
    ENUF GoToNextHubFlagID
    GOTO 3 //skip over the rest
LABEL 3
STAL 1
GotoPrepScreenHub
NoFade
ENDA

EndingScene:
MNC2 HubETheDecision
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN